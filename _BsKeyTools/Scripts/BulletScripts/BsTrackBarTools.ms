/*
 * @Description: è‡ªåŠ¨å¤‡ä»½å·¥å…·,å¤§æ”¹è‡ªSaveButtonè„šæœ¬
 * @Author: Bullet.S
 * @Date: 2021-05-08 17:18:12
 * @LastEditors: Bullet.S
 * @LastEditTime: 2023-06-08 11:28:15
 * @Email: animator.bullet@foxmail.com
 */


if form != undefined then (form.close();form = undefined)
try (cui.UnRegisterDialogBar rolBsTrackBarTools) catch()
try (destroyDialog rolBsTrackBarTools) catch()
try (destroydialog rolBsBackupManager) catch()
try (destroydialog rolBsStopTime) catch()

global rolBsTrackBarTools
global rolBsBackupManager
Global BulletConfig      = execute ("@\"" + (getDir #maxData) + "\\BulletConfig.ini\"")  --é…ç½®æ–‡ä»¶è·¯å¾„
global iniBackupPath     = ((getdir #autoback) + "\\BsBackup")
global iniMaxBackupCount = 60
global iniPosAutoBackup
Global iniPosBackupManager
global iniStopTime = #(21,9)
global iniStopState = false
global iniDockPos
global heightBsSize = 30
global widthBsSize  = 335
Global SIOFile      = dotNetClass "System.IO.File"
Global SIODir       = dotNetClass "System.IO.Directory"
global arrBsBackupFiles = #()

try(FileIn ((getDir #scripts) + "\\BulletScripts\\fnSaveLoadConfig.ms"))
catch(messagebox "è„šæœ¬å®‰è£…å¯èƒ½ä¸å®Œå…¨,å»ºè®®é‡æ–°å®‰è£…...        " beep:false)
try(FileIn ((getDir #scripts) + "\\BulletScripts\\fnGetColorTheme.ms"))
catch(messagebox "è„šæœ¬å®‰è£…å¯èƒ½ä¸å®Œå…¨,å»ºè®®é‡æ–°å®‰è£…...        " beep:false)
stLoadConfigAll.fnLoadConfigTrackBarTools()

fn fnDelFileDir targetDel =  --åˆ é™¤æ–‡ä»¶
(
	if (SIOFile.Exists targetDel == true) then ---åˆ¤æ–­æ˜¯å¦å­˜åœ¨æ–‡ä»¶
	(
		dotnet.loadAssembly "Microsoft.VisualBasic.dll"
	
		FileIO = dotnetclass "Microsoft.VisualBasic.FileIO.FileSystem"
		UIOption = (dotnetclass "Microsoft.VisualBasic.FileIO.UIOption").OnlyErrorDialogs
		RecycleOption = (dotnetclass "Microsoft.VisualBasic.FileIO.RecycleOption").SendToRecycleBin

		if getFileAttribute targetDel #readOnly == true or \
		getFileAttribute targetDel #hidden == true do --ä¿®æ”¹åªè¯»æˆ–è€…éšè—å±æ€§
		(
			setFileAttribute targetDel #readOnly false ; \
			setFileAttribute targetDel #hidden false
		)
		try 
		(
			FileIO.DeleteFile targetDel UIOption RecycleOption
			-- SIOFile.Delete(targetDel)
			(print ("å·²åˆ é™¤: "+ filenameFromPath targetDel  + " è‡³å›æ”¶ç«™"))
		)
		catch 
		(
			messagebox ("åˆ é™¤å¤±è´¥: "+ filenameFromPath targetDel + ". è¯·å°è¯•æ‰‹åŠ¨åˆ é™¤ã€‚          ") title:"BsTrackBarTools"
			(shellLaunch (getfilenamepath targetDel) "")
		)
	)
)
fn fnDelDir dirDel =
(
	if (SIODir.Exists dirDel) == true do
	(
		dotnet.loadAssembly "Microsoft.VisualBasic.dll"
	
		FileIO = dotnetclass "Microsoft.VisualBasic.FileIO.FileSystem"
		UIOption = (dotnetclass "Microsoft.VisualBasic.FileIO.UIOption").OnlyErrorDialogs
		RecycleOption = (dotnetclass "Microsoft.VisualBasic.FileIO.RecycleOption").SendToRecycleBin

		if getFileAttribute dirDel #readOnly == true or getFileAttribute dirDel #hidden == true do
		(
			setFileAttribute dirDel #readOnly false ; setFileAttribute dirDel #hidden false
		)
		try 
		(
			FileIO.DeleteDirectory dirDel UIOption RecycleOption
			-- SIODir.Delete(dirDel) true
			(print ("å·²åˆ é™¤: "+ pathConfig.stripPathToLeaf dirDel + " æ–‡ä»¶å¤¹è‡³å›æ”¶ç«™"))
		)
		catch 
		(
			messagebox ("åˆ é™¤å¤±è´¥: "+ pathConfig.stripPathToLeaf dirDel + " æ–‡ä»¶å¤¹. è¯·å°è¯•æ‰‹åŠ¨åˆ é™¤.") title:"BsTrackBarTools"
			(shellLaunch dirDel "")
		)
	)
)

fn fnLoadDockPos rolTarget wSize hSize =
(
	if doesFileExist BulletConfig do
	(
		try
		(
			nameAttrClass = substring rolTarget.name 4 rolTarget.name.count
			btnsArr = rolTarget.controls
			iniDockPos = fnGetConfig iniDockPos nameAttrClass "DockPos" "cui_dock_bottom"
			case iniDockPos of
			(
				"cui_dock_top":
				(
					try(cui.UnRegisterDialogBar rolTarget)catch()
					--	rearrange buttons to be horizontal
					for b = 1 to btnsArr.count where (classof btnsArr[b]) == dotNetControl do
					(
						case b of
						(
							(1):
							(
								btnsArr[b].pos = [0,0]
								btnsArr[b].height = 30
								btnsArr[b].width = 15
							)
							default:
							(
								btnsArr[b].pos = [btnsArr[b - 1].pos.x + btnsArr[b - 1].width,0]
							)
						)
					)
					rolTarget.height = hSize
					rolTarget.width = wSize
					cui.RegisterDialogBar rolTarget minSize:[wSize,hSize] maxSize:[wSize,hSize] style:#(#cui_dock_horz)
					cui.DockDialogBar rolTarget #cui_dock_top
					try(setINISetting BulletConfig nameAttrClass "DockPos" (#cui_dock_top as string))catch()
				)
				"cui_dock_bottom":
				(
					try(cui.UnRegisterDialogBar rolTarget)catch()
					--	rearrange buttons to be horizontal
					for b = 1 to btnsArr.count where (classof btnsArr[b]) == dotNetControl do
					(
						case b of
						(
							(1):
							(
								btnsArr[b].pos = [0,0]
								btnsArr[b].height = 30
								btnsArr[b].width = 15
							)
							default:
							(
								btnsArr[b].pos = [btnsArr[b - 1].pos.x + btnsArr[b - 1].width,0]
							)
						)
					)
					rolTarget.height = hSize
					rolTarget.width = wSize
					cui.RegisterDialogBar rolTarget minSize:[wSize,hSize] maxSize:[wSize,hSize] style:#(#cui_dock_horz)
					cui.DockDialogBar rolTarget #cui_dock_bottom
					try(setINISetting BulletConfig nameAttrClass "DockPos" (#cui_dock_bottom as string))catch()
				)
				"cui_dock_left":
				(
					try(cui.UnRegisterDialogBar rolTarget)catch()
					--	rearrange buttons to be vertical 
					for b = 1 to btnsArr.count where (classof btnsArr[b]) == dotNetControl do
					(
						case b of
						(
							(1):
							(
								btnsArr[b].pos = [0,0]
								btnsArr[b].height = 15
								btnsArr[b].width = hSize + 50
							)
							(4):
							(
								btnsArr[b].pos = [0,btnsArr[b - 1].pos.y + btnsArr[b - 1].height]
							)
							(5):
							(
								btnsArr[b].pos = [btnsArr[b - 1].width,btnsArr[b - 2].pos.y + btnsArr[b - 2].height]
							)
							default:
							(
								btnsArr[b].pos = [0,btnsArr[b - 1].pos.y + btnsArr[b - 1].height]
							)
						)
					)
					rolTarget.height = wSize + 50
					rolTarget.width = hSize + 50
					--
					cui.RegisterDialogBar rolTarget minSize:[hSize + 50,wSize] maxSize:[hSize + 50,wSize] style:#(#cui_dock_vert)
					cui.DockDialogBar rolTarget #cui_dock_left
					try(setINISetting BulletConfig nameAttrClass "DockPos" (#cui_dock_left as string))catch()
				)
				"cui_dock_right":
				(
					try(cui.UnRegisterDialogBar rolTarget)catch()
					--	rearrange buttons to be vertical 
					for b = 1 to btnsArr.count where (classof btnsArr[b]) == dotNetControl do
					(
						case b of
						(
							(1):
							(
								btnsArr[b].pos = [0,0]
								btnsArr[b].height = 15
								btnsArr[b].width = hSize + 50
							)
							(4):
							(
								btnsArr[b].pos = [0,btnsArr[b - 1].pos.y + btnsArr[b - 1].height]
							)
							(5):
							(
								btnsArr[b].pos = [btnsArr[b - 1].width,btnsArr[b - 2].pos.y + btnsArr[b - 2].height]
							)
							default:
							(
								btnsArr[b].pos = [0,btnsArr[b - 1].pos.y + btnsArr[b - 1].height]
							)
						)
					)
					rolTarget.height = wSize + 50
					rolTarget.width = hSize + 50
					cui.RegisterDialogBar rolTarget minSize:[hSize + 50,wSize] maxSize:[hSize + 50,wSize] style:#(#cui_dock_vert)
					cui.DockDialogBar rolTarget #cui_dock_right
					try(setINISetting BulletConfig nameAttrClass "DockPos" (#cui_dock_right as string))catch()
				)
				default:
				(
					try(cui.UnRegisterDialogBar rolTarget)catch()
					for b = 1 to btnsArr.count where (classof btnsArr[b]) == dotNetControl do
					(
						case b of
						(
							(1):
							(
								btnsArr[b].pos = [0,0]
								btnsArr[b].height = 30
								btnsArr[b].width = 15
							)
							default:
							(
								btnsArr[b].pos = [btnsArr[b - 1].pos.x + btnsArr[b - 1].width,0]
							)
						)
					)
					rolTarget.height = hSize
					rolTarget.width = wSize
					-- cui.RegisterDialogBar rolTarget style:#(#cui_dock_bottom,#cui_dock_top,#cui_floatable, #style_toolwindow) maxSize:[190,30]
					setDialogPos rolTarget (readvalue ((getINISetting BulletConfig nameAttrClass "PosTrackBarTools") as stringStream))
				)
			)
		)
		catch()
	)
)

rollout rolBsStopTime "BsBackup_æš‚åœæ—¶æ®µè®¾ç½®" width:180 height:135
(
	group ""
	(
		radiobuttons rdoTimeRange "" pos:[30,20] labels:#("ç¦ç”¨","æš‚åœæ—¶æ®µ") default:1
		
		spinner spiStartTimeH "ã€èµ·å§‹ã€‘  æ—¶" pos:[70,45] width:70 height:20 type:#integer range:[0,23,21] scale:1 enabled:false
		spinner spiEndTimeH "ã€ç»“æŸã€‘  æ—¶" pos:[70,70] width:70 height:20 type:#integer range:[0,23,9] scale:1 enabled:false

		button btnApply "ç¡®å®š" pos:[20,95] width:140 height:25
	)
	
	fn fnChangeVisibleState =
	(
		if rdoTimeRange.state == 2 then 
		(
			iniStopState = true
		)
		else
		(
			iniStopState = false
		)
		stSetConfigAll.fnSetConfigTrackBarTools()
		spiStartTimeH.enabled = spiEndTimeH.enabled = iniStopState
		return iniStopState
	)
	
	on rolBsStopTime open do
	(
		stLoadConfigAll.fnLoadConfigTrackBarTools()
		stSetConfigAll.fnSetConfigTrackBarTools()
		if iniStopState then rdoTimeRange.state = 2 else rdoTimeRange.state = 1
		fnChangeVisibleState()
		spiStartTimeH.value = iniStopTime[1]
		spiEndTimeH.value   = iniStopTime[2]
	)

	on rolBsStopTime close do
	(
		stSetConfigAll.fnSetConfigTrackBarTools()
	)
	
	on rdoTimeRange changed state do 
	(
		fnChangeVisibleState()
	)

	on spiStartTimeH changed val do
	(
		iniStopTime[1] = val
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on spiEndTimeH changed val do
	(
		iniStopTime[2] = val
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on btnApply pressed do 
	(
		try (destroydialog rolBsStopTime) catch ()
	)
)

rcmenu menuBsTrackBarTools
(
	submenu "åœé ä½ç½®"
	(
		menuItem mItemTop "ä¸Š" checked:(if rolBsTrackBarTools.dialogBar and (cui.getDockState rolBsTrackBarTools) == "cui_dock_top" then true else false)
		menuItem mItemBottom "ä¸‹" checked:(if rolBsTrackBarTools.dialogBar and (cui.getDockState rolBsTrackBarTools) == "cui_dock_bottom" then true else false)
		menuItem mItemLeft "å·¦" checked:(if rolBsTrackBarTools.dialogBar and (cui.getDockState rolBsTrackBarTools) == "cui_dock_left" then true else false)
		menuItem mItemRight "å³" checked:(if rolBsTrackBarTools.dialogBar and (cui.getDockState rolBsTrackBarTools) == "cui_dock_right" then true else false)
		separator menuSepDock
		menuItem mItemFloat "æµ®" checked:(if not rolBsTrackBarTools.dialogBar then true else false)
	)

	submenu "å¤‡ä»½æ•°é‡"
	(
		menuItem mItemBackupInfinite "æ— é™åˆ¶" checked:(if iniMaxBackupCount == 0 then true else false)
		separator mItemBackupCount
		menuItem mItemBackup3 "3" checked:(if iniMaxBackupCount == 3 then true else false)
		menuItem mItemBackup7 "7" checked:(if iniMaxBackupCount == 7 then true else false)
		menuItem mItemBackup24 "24" checked:(if iniMaxBackupCount == 24 then true else false)
		menuItem mItemBackup40 "40" checked:(if iniMaxBackupCount == 40 then true else false)
		menuItem mItemBackup60 "60" checked:(if iniMaxBackupCount == 60 then true else false)
		menuItem mItemBackup80 "80" checked:(if iniMaxBackupCount == 80 then true else false)
		menuItem mItemBackup100 "100" checked:(if iniMaxBackupCount == 100 then true else false)
	)

	submenu "è·¯å¾„é…ç½®"
	(
		menuItem mItemBackupPath "è®¾å®š"
		menuItem mItemResetPath "é‡ç½®"
	)

	menuItem mItemStopTime "æš‚åœæ—¶æ®µ"
	
	menuItem mItemIsStartup "è‡ªå¯å¼€å…³"
	separator menuSepClose
	menuItem mItemClose "å…³é—­"

	local startupPath            = (getDir #StartupScripts)+ "\\BsTrackBarToolsStartup.ms"
	local startupBsTrackBarTools = (getDir #Scripts) + "\\BulletScripts\\StartupMS\\BsTrackBarToolsStartup.ms"

	on menuBsTrackBarTools open do
	(
		if(SIOFile.Exists startupPath) then (mItemIsStartup.checked = true)
		else (mItemIsStartup.checked = false)
	)

	on mItemStopTime picked do
	(
		try (destroydialog rolBsStopTime) catch ()
		if (iniPosBackupManager != 0) then 
		(Createdialog rolBsStopTime fgcolor:myFgColor pos:iniPosBackupManager style:#(#style_titlebar, #style_sysmenu, #style_toolwindow))
		else (Createdialog rolBsStopTime fgcolor:myFgColor style:#(#style_titlebar, #style_sysmenu, #style_toolwindow))
	)

	on mItemBackupPath picked do 
	(
		local dirOpened = getSavePath caption:"è¯·é€‰æ‹©å¤‡ä»½è·¯å¾„: " initialDir:iniBackupPath

        if (dirOpened != undefined) then 
		(
			iniBackupPath = dirOpened
			rolBsBackupManager.edtBackupPath.text = iniBackupPath
			rolBsBackupManager.fnRefreshBackupAll()
			stSetConfigAll.fnSetConfigTrackBarTools()
		)
	)

	on mItemResetPath picked do 
	(
		iniBackupPath = ((getdir #autoback) + "\\BsBackup")
		rolBsBackupManager.edtBackupPath.text = iniBackupPath
		rolBsBackupManager.fnRefreshBackupAll()
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on mItemIsStartup picked do
	(
		if (mItemIsStartup.checked == true) then 
		(
			fnDelFileDir startupPath
			mItemIsStartup.checked = false
			messagebox ("å·²è§£é™¤ BsTrackBarTools è‡ªå¯ï¼                  ")  title:"è‡ªå¯è®¾ç½®"
		)
		else 
		(
			if (not (SIOFile.Exists startupPath)) do
			(
				try 
				(
					-- SIOFile.Copy startupBsTrackBarTools startupPath
					FileIO.Copyfile startupBsTrackBarTools startupPath UIOption
					messagebox ("å·²æ‰“å¼€ BsTrackBarTools è‡ªå¯ï¼                  ")  title:"è‡ªå¯è®¾ç½®"
					mItemIsStartup.checked = true
					setFileAttribute startupPath #readOnly true
				)
				catch
				(
					-- messagebox ("è‡ªå¯å¤±è´¥ï¼Œçƒ¦è¯·ç®¡ç†å‘˜è¿è¡Œ max æˆ–æ‰‹åŠ¨å¤„ç†ï¼š\r\n\r\næŠŠ StartupMS\\BsTrackBarToolsStartup.ms å¤åˆ¶åˆ°\r\n\r\n" + (getDir #StartupScripts) + "                    ")
					-- (shellLaunch (getfilenamepath startupBsTrackBarTools) "")
				)
			)
		)
	)

	on mItemTop picked do
	(
		if not mItemTop.checked then 
		(
			if iniDockPos == "float" then (iniPosAutoBackup = (GetDialogPos rolBsTrackBarTools))
			iniDockPos = "cui_dock_top"
			stSetConfigAll.fnSetConfigTrackBarTools()
			fnLoadDockPos rolBsTrackBarTools widthBsSize heightBsSize
			if (rolBsTrackBarTools.SaveInterval > 0) then rolBsTrackBarTools.clock.active = true
		)
	)

	on mItemBottom picked do
	(
		if not mItemBottom.checked then 
		(
			if iniDockPos == "float" then (iniPosAutoBackup = (GetDialogPos rolBsTrackBarTools))
			iniDockPos = "cui_dock_bottom"
			stSetConfigAll.fnSetConfigTrackBarTools()
			fnLoadDockPos rolBsTrackBarTools widthBsSize heightBsSize
			if (rolBsTrackBarTools.SaveInterval > 0) then rolBsTrackBarTools.clock.active = true
		)
	)

	on mItemLeft picked do
	(
		if not mItemLeft.checked then 
		(
			if iniDockPos == "float" then (iniPosAutoBackup = (GetDialogPos rolBsTrackBarTools))
			iniDockPos = "cui_dock_left"
			stSetConfigAll.fnSetConfigTrackBarTools()
			fnLoadDockPos rolBsTrackBarTools widthBsSize heightBsSize
			if (rolBsTrackBarTools.SaveInterval > 0) then rolBsTrackBarTools.clock.active = true
		)
	)

	on mItemRight picked do
	(
		if not mItemRight.checked then 
		(
			if iniDockPos == "float" then (iniPosAutoBackup = (GetDialogPos rolBsTrackBarTools))
			iniDockPos = "cui_dock_right"
			stSetConfigAll.fnSetConfigTrackBarTools()
			fnLoadDockPos rolBsTrackBarTools widthBsSize heightBsSize
			if (rolBsTrackBarTools.SaveInterval > 0) then rolBsTrackBarTools.clock.active = true
		)
	)

	on mItemFloat picked do
	(
		if not mItemFloat.checked then 
		(
			iniDockPos = "float"
			stSetConfigAll.fnSetConfigTrackBarTools()
			fnLoadDockPos rolBsTrackBarTools widthBsSize heightBsSize
			if (rolBsTrackBarTools.SaveInterval > 0) then rolBsTrackBarTools.clock.active = true
		)
	)

	on mItemClose picked do
	(
		if form != undefined then ( form.close(); form = undefined  )
		try (cui.UnRegisterDialogBar rolBsTrackBarTools) catch()
		try (destroyDialog rolBsTrackBarTools) catch()
	)

	on mItemBackup4 picked do 
	(
		iniMaxBackupCount = 4
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on mItemBackup7 picked do 
	(
		iniMaxBackupCount = 7
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on mItemBackup24 picked do 
	(
		iniMaxBackupCount = 24
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on mItemBackup40 picked do 
	(
		iniMaxBackupCount = 40
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on mItemBackup60 picked do 
	(
		iniMaxBackupCount = 60
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on mItemBackup80 picked do 
	(
		iniMaxBackupCount = 80
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on mItemBackup100 picked do 
	(
		iniMaxBackupCount = 100
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on mItemBackupInfinite picked do 
	(
		iniMaxBackupCount = 0
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

)

rollout rolBsTrackBarTools "BsTrackBarTools_v1.1" 
(
	local form, mcolor, rcLIST,  SaveInterval, saveInterval_FN , autobackup
	local INIfile = ( getDir #maxData ) + "\\BulletConfig.ini"
	local mcolor = dotnetclass "System.drawing.color" 
	local malign = dotnetclass "System.Drawing.ContentAlignment"
	
	local dnFontStyle = dotNetClass "System.Drawing.FontStyle"
	local myFontStyle = dotnet.combineenums dnFontStyle.bold --dnFontStyle.italic

	dotnetcontrol btnConfig "Button" height:30 width:15 pos:[0,0]
	dotnetcontrol btnFilesCount "Button" height:30 width:80 pos:[15,0]
	-- dotnetcontrol btnUndock "Button" height:40 width:15 pos:[0,0]
	-- dotnetcontrol btnDockTop "Button" height:15 width:15 pos:[15,0]
	-- dotnetcontrol btnDockBottom "Button" height:15 width:15 pos:[15,15]
	dotnetcontrol btnTimer "Button" height:30 width:80 pos:[95,0] --replace
	dotnetcontrol btnBackup "Button" height:30 width:40 pos:[175,0] --increment
	dotnetcontrol btnSaveAs "Button" height:30 width:40 pos:[215,0]  --save as
	dotnetcontrol btnBackupManager "Button" height:30 width:80 pos:[255,0]  --æ‰“å¼€å¤‡ä»½æ–‡ä»¶å¤¹
	
	timer clock "testClock" interval:1000 active:false  --tick once a second

	fn fnButtonStyle tarButton textButton =
	(
		tarButton.text = textButton
		tarButton.flatstyle = dotnetobject "System.Windows.Forms.FlatStyle" Flat
		tarButton.FlatAppearance.BorderColor = mcolor.gray 
		tarButton.FlatAppearance.MouseOverBackColor = BsDotCheckColor
		tarButton.backcolor = BsDotBackColor
		tarButton.ForeColor = BsDotForeColor
	)

	fn saveInterval_FN TimeInterval = 
	(
		clock.active = true -- si le prï¿½cï¿½dent == 0, on active
		
		setINISetting INIfile "SaveButton" "SaveInterval" TimeInterval  forceUTF16:false --force ASCII
		
		SaveInterval = TimeInterval as integer
		
		if clock.ticks < SaveInterval*60 then
		(
			btnTimer.backcolor = BsDotCheckColor
			btnTimer.FlatAppearance.MouseOverBackColor = BsDotCheckColor
		)
	)
	
	fn SaveMenu = --rightCLIC 
	(
		rcMenu rcLIST
		(
			menuitem m_off "å…³é—­è®¡æ—¶" 		checked:(if SaveInterval == 0 then true else false)
			menuitem m_restart "é‡æ–°è®¡æ—¶" 		 checked:false
			menuItem m_autoback "è‡ªåŠ¨å¤‡ä»½" checked:(if autobackup == "true" then true else false)
			
			separator sep1  
			
			menuitem m_1 "1 åˆ†é’Ÿ" 	checked:(if SaveInterval == 1 then true else false)
			menuitem m_3 "3 åˆ†é’Ÿ" 	checked:(if SaveInterval == 3 then true else false)
			menuitem m_5 "5 åˆ†é’Ÿ" 	checked:(if SaveInterval == 5 then true else false)
			menuItem m_10 "10 åˆ†é’Ÿ"	checked:(if SaveInterval == 10 then true else false)
			menuItem m_15 "15 åˆ†é’Ÿ"	checked:(if SaveInterval == 15 then true else false)
			menuItem m_20 "20 åˆ†é’Ÿ"	checked:(if SaveInterval == 20 then true else false)
			menuItem m_30 "30 åˆ†é’Ÿ"	checked:(if SaveInterval == 30 then true else false)
			menuItem m_45 "45 åˆ†é’Ÿ"	checked:(if SaveInterval == 45 then true else false)
			menuItem m_60 "60 åˆ†é’Ÿ"	checked:(if SaveInterval == 60 then true else false)
			
			on m_off picked do 	
			( 
				btnTimer.text = "å…³é—­è®¡æ—¶" 
				saveInterval_FN "0" 
				
				btnTimer.backcolor = BsDotCheckColor
				btnTimer.FlatAppearance.MouseOverBackColor = BsDotCheckColor
				
				clock.active = false
				clock.ticks = 0
			)

			on m_autoback picked do 
			(
				m_autoback.checked = not m_autoback.checked
				autobackup = (m_autoback.checked as string)
				setINISetting INIfile "SaveButton" "Autobackup" autobackup
			)
			
			on m_1 picked do 	( saveInterval_FN "1"  )
			on m_3 picked do 	( saveInterval_FN "3"  )
			on m_5 picked do 	( saveInterval_FN "5"  )
			on m_10 picked do 	( saveInterval_FN "10"  )
			on m_15 picked do 	( saveInterval_FN "15"  )
			on m_20 picked do 	( saveInterval_FN "20"  )
			on m_30 picked do 	( saveInterval_FN "30"  )
			on m_45 picked do 	( saveInterval_FN "45"  )
			on m_60 picked do 	( saveInterval_FN "60"  )

			on m_restart picked do
			(
				clock.ticks = 0
				
				btnTimer.backcolor = BsDotCheckColor
				btnTimer.FlatAppearance.MouseOverBackColor = BsDotCheckColor
			)
		)
	)

	fn fnFilesCount = 
	(
		tarPath = maxFilePath
		numFiles = 0
		if maxFileName != "" then
		(
			tarPath = (iniBackupPath + "\\" + (getFilenameFile maxFileName) + ".backup")
			numFiles = (getfiles (tarPath + "\\*.max")).count
			if numFiles != 0 then (numFiles = numFiles + 1) else (numFiles = 0)
		)
		else (fnButtonStyle rolBsTrackBarTools.btnFilesCount ("æš‚æ— å¤‡ä»½"))
		if numFiles == 0 then 
		(
			fnButtonStyle rolBsTrackBarTools.btnFilesCount ("æš‚æ— å¤‡ä»½")
		)
		else 
		(
			fnButtonStyle rolBsTrackBarTools.btnFilesCount ("å½“å‰ç‰ˆæœ¬" + numFiles as string)
		)
		rolBsTrackBarTools.btnFilesCount.FlatAppearance.BorderColor = mcolor.gray 
	)
	
	fn fnIsStopTime =
	(
		local isStopTime = false

		if iniStopState then 
		(
			local dateTime = (dotNetClass "System.DateTime").Now
			local curHour = dateTime.Hour
	
			timeStart = iniStopTime[1]
			timeEnd = iniStopTime[2]
	
			case of 
			(
				(iniStopTime[2] == iniStopTime[1]):
				(
					isStopTime = (if curHour == iniStopTime[2] then true else false)
				)
				(iniStopTime[2] < iniStopTime[1]):
				(
					if (curHour >= iniStopTime[1] and curHour <= 23) or \
					(curHour >= 0 and curHour <= iniStopTime[2]) then 
					(
						isStopTime = true
					)
					else (isStopTime = false)
				)
				(default):
				(
					if (curHour >= iniStopTime[1] and curHour <= iniStopTime[2]) then
					(
						isStopTime = true
					)
					else (isStopTime = false)
				)
			)
		)
		return isStopTime
	)

	fn fnTrackBarQuickSave =
	(
		if objects.count != 0 and getSaveRequired() then 
		(
			if maxFilePath == "" then 
			(
				-- messagebox "------------------------------------\r\n\r\nå½“å‰åœºæ™¯æœªä¿å­˜è¿‡,\r\n\r\nè¯·å…ˆä¿å­˜åˆå§‹ç‰ˆæœ¬~\r\n\r\n------------------------------------"
				-- -- max file saveas
				-- btnTimer.text = "è¯·å…ˆä¿å­˜" 
				-- saveInterval_FN "0" 
				
				-- btnTimer.backcolor = BsDotCheckColor
				-- btnTimer.FlatAppearance.MouseOverBackColor = BsDotCheckColor
				
				-- clock.active = false
				-- clock.ticks = 0

				local strCurFilePath    = (iniBackupPath + "\\@BsAutoBackup.max")
				local strCurFileName    = "@BsAutoBackup"
			)
			else
			(
				local strCurFilePath    = (maxFilePath + maxFileName)
				local strCurFileName    = getFilenameFile maxFileName
			)
			local strBackupPathRoot = iniBackupPath + "\\" + strCurFileName + ".backup"
			local arrBackupFiles = getfiles (strBackupPathRoot + "\\*.max")
			qsort arrBackupFiles fnPseudoNaturalSort
			-- print arrBackupFiles
			if iniMaxBackupCount != 0 then
			(
				if arrBackupFiles.count >= iniMaxBackupCount then 
				(
					for i = 1 to (arrBackupFiles.count - iniMaxBackupCount + 1) do 
					(
						fnDelFileDir arrBackupFiles[i]
					)
				)
				-- print iniMaxBackupCount
				-- print arrBackupFiles.count
			)

			if matchpattern strCurFilePath pattern:("*.backup\\*.max") then 
			(
				strBackupPathRoot = maxFilePath
				arrBackupFiles = getfiles (strBackupPathRoot + "\\*.max")
				qsort arrBackupFiles fnPseudoNaturalSort
				strSaveFileName   = getFilenameFile arrBackupFiles[arrBackupFiles.count]
				arrEndCountFilter = (filterString strSaveFileName "_")
				strSaveCount      = ((execute arrEndCountFilter[arrEndCountFilter.count])) as string
				strSaveName       = substring strSaveFileName 1 (strSaveFileName.count - strSaveCount.count - 1)
				strSaveCount      = ((execute strSaveCount) + 1) as string
				strBackupFilePath = strBackupPathRoot + "\\" + strSaveName + "_" + strSaveCount + ".max"
				saveMaxFile strBackupFilePath
				displayTempPrompt ("å·²å¤‡ä»½å¹¶æ‰“å¼€ï¼š" + strBackupFilePath) 10000
				messagebox ("å½“å‰å¤‡ä»½çš„æ˜¯å·²å¤‡ä»½æ–‡ä»¶ï¼Œè¯·æ³¨æ„è·Ÿæºæ–‡ä»¶å¯¹æ¯”æ–°æ—§ï¼\r\n\r\nå¯èƒ½çš„æºæ–‡ä»¶ï¼š" + strSaveName + ".max\r\n\r\nåˆšæ‰å¤‡ä»½çš„æ–‡ä»¶ï¼š" + strCurFilePath + ".max                                                                           ") title:"BsTrackBarTools"
			) 
			else
			(
				if (not (SIODir.Exists strBackupPathRoot))
				do (SIODir.CreateDirectory strBackupPathRoot)
				if arrBackupFiles.count == 0 then 
				(
					strSaveCount = "1"
				) 
				else if arrBackupFiles.count > 0 then
				(
					strSaveFileName   = getFilenameFile arrBackupFiles[arrBackupFiles.count]
					arrEndCountFilter = (filterString strSaveFileName "_")
					strSaveCount      = ((execute arrEndCountFilter[arrEndCountFilter.count]) + 1) as string
				)
				strBackupFilePath = strBackupPathRoot + "\\" + strCurFileName + "_" + strSaveCount + ".max"
				-- saveMaxFile strCurFilePath
				if (not (SIOFile.Exists strBackupFilePath)) do
				(
					try 
					(
						saveMaxFile strBackupFilePath clearNeedSaveFlag:false useNewFile:false
						displayTempPrompt ("å·²å¤‡ä»½è‡³ï¼š" + strBackupFilePath) 10000 
					)
					catch()
				)
			)
			rolBsTrackBarTools.fnFilesCount()
			rolBsBackupManager.fnRefreshBackupAll()
		)
		else (displayTempPrompt ("å½“å‰æ–‡ä»¶æ— å†…å®¹æˆ–æ— æ”¹åŠ¨ï¼Œä¸éœ€è¦è‡ªåŠ¨å¤‡ä»½ï¼Œä¹Ÿå¯ç‚¹å‡»è¿­ä»£å¼ºåˆ¶å¤‡ä»½ï¼") 10000)
	)
	
	on rolBsTrackBarTools open do
	(
		stLoadConfigAll.fnLoadConfigTrackBarTools()
		stSetConfigAll.fnSetConfigTrackBarTools()
		fnLoadDockPos rolBsTrackBarTools widthBsSize heightBsSize
		callbacks.removeScripts #filePostOpen id:#UpdateBsBackup
		callbacks.removeScripts #filePostSave id:#UpdateBsBackup
		callbacks.removeScripts #systemPostReset id:#UpdateBsBackup
		callbacks.addScript #systemPostReset "rolBsTrackBarTools.fnFilesCount()" id:#UpdateBsBackup
		callbacks.addScript #filePostOpen "rolBsTrackBarTools.fnFilesCount()" id:#UpdateBsBackup
		callbacks.addScript #filePostSave "rolBsTrackBarTools.fnFilesCount()" id:#UpdateBsBackup
		callbacks.removeScripts id:#SaveButtonCallback
		callbacks.addScript #filePostOpenProcess "rolBsTrackBarTools.clock.ticks = 0" id:#SaveButtonCallback
		
		callbacks.removeScripts id:#SaveButtonResetCallback
		callbacks.addScript #systemPostReset "rolBsTrackBarTools.clock.ticks = 0" id:#SaveButtonResetCallback
		SaveMenu() 
		Inter = ( getINISetting INIfile "SaveButton" "SaveInterval" ) 
		autobackup = ( getINISetting INIfile "SaveButton" "Autobackup" ) 
		if autobackup == "" then (setINISetting INIfile "SaveButton" "Autobackup" "false";autobackup = "false")

		if Inter == "" then
		( 
			setINISetting INIfile "SaveButton" "SaveInterval" "15" forceUTF16:false --force ASCII
			
			SaveInterval = 15
			clock.active = true
		)
		else
		(
			if Inter as integer != 0 then (saveInterval_FN Inter)
			else 
			(
				SaveInterval = 0
				clock.active = false
				btnTimer.text = "å…³é—­è®¡æ—¶"
			)
		)
		-- fnButtonStyle btnDockTop "â–²"
		-- fnButtonStyle btnDockBottom "â–¼"
		fnButtonStyle btnTimer "æ¿€æ´»å¤‡ä»½"
		btnTimer.font = dotNetObject "System.Drawing.Font" "Arial" 9 myFontStyle
		fnButtonStyle btnBackup "è¿­ä»£"
		fnButtonStyle btnSaveAs "å¦å­˜"
		fnButtonStyle btnBackupManager "å¤‡ä»½ç®¡ç†"
		fnFilesCount()
		fnButtonStyle btnConfig ""
		local objToolTip = dotnetobject "System.Windows.Forms.ToolTip"  
		objToolTip.SetToolTip btnFilesCount "å·¦é”®æ‰“å¼€é¢æ¿ï¼Œå³é”®æ‰“å¼€æ–‡ä»¶å¤¹"
		objToolTip.Active = True
		objToolTip.tooltipicon = (dotnetclass "System.Windows.Forms.ToolTipIcon").none
		-- other types of tooltip icon are 
		--.Error
		-- .info
		-- .none
		btnConfig.BackColor = BsDotCheckColor
		
	)
	
	on rolBsTrackBarTools close do
	(
		iniPosAutoBackup = (GetDialogPos rolBsTrackBarTools)
		-- iniDockPos = ((cui.getDockState rolBsTrackBarTools) as string)
		-- print iniDockPos
		callbacks.removeScripts #filePostOpen id:#UpdateBsBackup
		callbacks.removeScripts #filePostSave id:#UpdateBsBackup
		stSetConfigAll.fnSetConfigTrackBarTools()
		callbacks.removeScripts id:#SaveButtonCallback
		callbacks.removeScripts id:#SaveButtonResetCallback
		clock.active = false
		
		if form != undefined then ( form.close(); form = undefined  )
	)
	
	on btnConfig mouseDown senderArg arg do
	(
		popupMenu menuBsTrackBarTools pos:[mouse.screenpos.x,mouse.screenpos.y + 20]
	)
	
	on btnTimer mouseDown senderArg arg do -- replace
	(
		if arg.button == arg.button.left or arg.button == arg.button.right do
		(
			-- sceneName = maxFilePath + maxFileName
			-- saveMaxFile sceneName quiet:true
				
			-- clearlistener()
			-- print "æ–‡ä»¶å·²è¦†ç›–ä¿å­˜"

			
			-- clock.ticks = 0
			
			-- btnTimer.backcolor = BsDotCheckColor
			-- btnTimer.FlatAppearance.MouseOverBackColor = BsDotCheckColor

			popUpMenu rcLIST pos:( [arg.x  , arg.y - 10 ] ) rollout:rolBsTrackBarTools align:#align_topleft
		)
		-- if arg.button == arg.button.right do
		-- (
			
		-- )
		
	)
	
	on btnBackup mouseDown senderArg arg do -- incremental
	(
		if arg.button == arg.button.left do
		(
			setSaveRequired true

			fnTrackBarQuickSave()

			clock.ticks = 0
			
			btnTimer.backcolor = BsDotCheckColor
			btnTimer.FlatAppearance.MouseOverBackColor = BsDotCheckColor
		)
-- 		
	)
	
	on btnSaveAs mouseDown senderArg arg do --save as
	(
		if arg.button == arg.button.left do
		(
			theName = getMAXSaveFileName() 
			
			if theName != undefined then
			(
				saveMaxFile theName
				
				clock.ticks = 0
				
				btnTimer.backcolor = BsDotCheckColor
				btnTimer.FlatAppearance.MouseOverBackColor = BsDotCheckColor
				
				clearlistener()
				print "ä¿å­˜æˆåŠŸ"
			)
			else
			(
				clearlistener()
				print "ä¿å­˜å¤±è´¥"
			)
		)
	)

	on btnFilesCount MouseDown senderArg arg do
	(
		if arg.button == btnFilesCount.mousebuttons.left then
		(
			if maxFileName != "" then
			(
				if matchpattern (maxFilePath + maxFileName) pattern:("*.backup\\*.max") then 
				(
					try(shellLaunch maxfilepath "")catch()
				)
				else
				(
					try(shellLaunch (iniBackupPath + "\\" + (getFilenameFile maxFileName) + ".backup") "")catch()
				)
			)
			else (try(shellLaunch iniBackupPath "")catch())
		)
	)

	on btnBackupManager MouseDown senderArg arg do
	(
		if arg.button == btnFilesCount.mousebuttons.left then
		(
			try (destroydialog rolBsBackupManager) catch ()
			if (iniPosBackupManager != 0) then (Createdialog rolBsBackupManager fgcolor:myFgColor pos:iniPosBackupManager style:#())
			else (Createdialog rolBsBackupManager fgcolor:myFgColor style:#())
		)
	)

	
	on clock tick do
	(
		if not fnIsStopTime() then
		(
			if SaveInterval > 0 do
			(
				Inter = SaveInterval * 60
				
				T = clock.ticks
				
				(
					if T<60 then ( TimeTXT = "00 : 00 : " + T as string  )
					else
					(
						if T > 3600 then --display Hours, min, secs
						(
							hours = T/3600 as integer
							
							minREST = ( T - hours*3600 )
							mins = minREST / 60 as integer
							
							secs = minREST - mins*60
							
							TimeTXT = hours as string + " : " + mins as string + " : " + secs as string
						)
						else --display mins secs
						(
						
							mins = T/60 as integer
							secs = T - mins*60
							
							TimeTXT = "0 : " + mins as string + " : " + secs as string
						)
					)
					
					btnTimer.text = TimeTXT
					
				)
				
				if T == Inter do
				(
					if autobackup == "true" then
					(
						fnTrackBarQuickSave()
						clock.ticks = 0
				
						btnTimer.backcolor = BsDotCheckColor
						btnTimer.FlatAppearance.MouseOverBackColor = BsDotCheckColor
					)
				)

				if T > Inter do
				(
					
					if (mod T 2)==0 then
					(
						COL = mcolor.Red
						
						btnTimer.text = "è¯·ä¿å­˜ !!"
					)
					else
					(
						COL = BsDotBackColor
						
						btnTimer.text = TimeTXT
					)
					OverCOL = mcolor.darkred

					btnTimer.backcolor = COL
					btnTimer.FlatAppearance.MouseOverBackColor = OverCOL
					
				)
			)
		)
		else btnTimer.text = "æš‚åœæ—¶æ®µ"
	)
)

if (iniPosAutoBackup != 0) then (createDialog rolBsTrackBarTools widthBsSize heightBsSize pos:iniPosAutoBackup style:#(#style_sysmenu,#style_toolwindow))
else (createDialog rolBsTrackBarTools widthBsSize heightBsSize style:#(#style_sysmenu,#style_toolwindow))

-- cui.RegisterDialogBar rolBsTrackBarTools style:#(#cui_dock_bottom,#cui_dock_top,#cui_floatable, #style_toolwindow) maxSize:[190,30]
-- cui.DockDialogBar rolBsTrackBarTools  #cui_dock_top
if (rolBsTrackBarTools.SaveInterval > 0) then rolBsTrackBarTools.clock.active = true


rcmenu menuBsFiles
(
	menuItem mItemOpenFile "æ‰“å¼€æ–‡ä»¶"
	menuItem mItemDelete "åˆ é™¤æ–‡ä»¶"
	menuItem mItemOpenMenu "æ‰“å¼€ç›®å½•"

	on mItemOpenFile picked do 
	(
		if (rolBsBackupManager.mlbBackupFiles.selection as array).count == 1 then 
		(
			tarFile = arrBsBackupFiles[(rolBsBackupManager.mlbBackupFiles.selection as array)[1]]
			if (doesfileexist tarFile) then
			(
				if CheckForSave() then (loadMaxFile tarFile useFileUnits:true)
			)
		)
	)

	on mItemDelete picked do 
	(
		if (queryBox "ç¡®å®šåˆ é™¤é€‰ä¸­çš„å·²å¤‡ä»½æ–‡ä»¶ï¼Ÿ                        " title:"BsTrackBarTools" beep:false) then
		(
			arrFileDelete = (rolBsBackupManager.mlbBackupFiles.selection as array)
			for i = 1 to arrFileDelete.count do 
			(
				fnDelFileDir arrBsBackupFiles[i]
			)
			rolBsBackupManager.fnRefreshFilesList()
		)
	)

	on mItemOpenMenu picked do 
	(
		if (rolBsBackupManager.mlbBackupFiles.selection as array).count == 1 then 
		(
			tarFile = arrBsBackupFiles[(rolBsBackupManager.mlbBackupFiles.selection as array)[1]]
			pathFile = (getfilenamepath tarFile)
			if (doesdirectoryexist pathFile) then
			(
				try(shellLaunch pathFile "")catch()
			)
		)
	)
)

rcmenu menuBsFolder
(
	menuItem mItemOpenFile "æ‰“å¼€å¤‡ä»½åº“"
	menuItem mItemDelete "åˆ é™¤å¤‡ä»½åº“"

	on mItemOpenFile picked do 
	(
		if (rolBsBackupManager.mlbBackupFolders.selection as array).count == 1 then 
		(
			idFolderSelected = (rolBsBackupManager.mlbBackupFolders.selection as array)[1]
			nameFolderSelected = (rolBsBackupManager.mlbBackupFolders.items)[idFolderSelected]
			nameFolderSelected = (substituteString nameFolderSelected "ğŸ“‚ " "")
			tarFolder = (rolBsBackupManager.edtBackupPath.text + "\\" + nameFolderSelected + ".backup")
			if (doesdirectoryexist tarFolder) then
			(
				try(shellLaunch tarFolder "")catch()
			)
		)
	)

	on mItemDelete picked do 
	(
		if (rolBsBackupManager.mlbBackupFolders.selection as array).count != 0 then 
		(
			if (queryBox "ç¡®å®šåˆ é™¤é€‰ä¸­å¤‡ä»½åº“å’Œå½“ä¸­çš„ã€æ‰€æœ‰ã€‘æ–‡ä»¶ï¼Ÿ                        " title:"BsTrackBarTools" beep:false) then
			(
				with undo on 
				(
					arrDirDelete = #()
					for i in (rolBsBackupManager.mlbBackupFolders.items as array) do 
					(
						nameFolder = (substituteString i "ğŸ“‚ " "")
						append arrDirDelete (rolBsBackupManager.edtBackupPath.text + "\\" + nameFolder + ".backup")
					)
					arrDirID = (rolBsBackupManager.mlbBackupFolders.selection as array)
					for i in arrDirID do 
					(
						fnDelDir arrDirDelete[i]
					)
					rolBsBackupManager.fnRefreshBackupAll()
					rolBsTrackBarTools.fnFilesCount()
				)
			)
		)
	)
)

rollout rolBsBackupManager "" width:430 height:320
(
	local posMouMove     = [0,0]
	local switchMouState = false

	groupbox grpMain " BsBackupManager_v1.1" \
	width:(rolBsBackupManager.width - 10) height:(rolBsBackupManager.height - 5) pos:[5,0]
	button btnClose "X" pos:[rolBsBackupManager.width - 15,0] height:15 width:15
	button btnRefreshFolder "R" align:#left border:false \
	height:20 width:25 pos:[10,20] tooltip:"å·¦ï¼šåˆ·æ–°å½“å‰æ–‡ä»¶å’Œå¤‡ä»½ç›®å½•\r\nå³ï¼šåˆ·æ–° 3dsMax è‡ªåŠ¨ä¿å­˜ç›®å½•"
	button btnOpenCurrentDir "æ‰“å¼€" tooltip:"æ‰“å¼€å¤‡ä»½æ–‡ä»¶å¤¹" \
	height:20 width:25 pos:[35,20] border:false
	editText edtBackupPath "" text:"" labelOnTop:true align:#left \
	height:20 fieldWidth:355 pos:[65,20] readOnly:true 
	-- button btnPrevFolder "â†‘" tooltip:"è¿”å›ä¸Šå±‚ç›®å½•" \
	-- height:20 width:25 pos:[340,20] border:false
	editText edtInputFilter "" text:"" labelOnTop:true align:#left \
	height:17 fieldWidth:375 pos:[10,47]
	checkbutton ckbReverse "â†‘ â†“" pos:[390,47] height:18 width:30
	MultiListBox mlbBackupFolders "" align:#left selection:0 \
	height:15 width:170 pos:[10,70]
	MultiListBox mlbBackupFiles "" align:#right selection:0 \
	height:18 width:240 pos:[180,70]
	label lblCountFolder "" height:15 width:120 pos:[15,275]
	label lblCountFiles "" height:15 width:120 pos:[15,295]

	fn fnGetLastFolder strFolder =
	(
		if ((strFolder != iniPosAutoBackup) and (doesDirectoryExist strFolder)) then
		(
			local arrFilterStr = filterstring strFolder @"\"
			local strLastFolder = ""
			for i = 1 to (arrFilterStr.count - 1) do 
			(
				strLastFolder = strLastFolder  + arrFilterStr[i] + @"\\"
			)
		)
		return strLastFolder
	)

	fn fnRefreshFolderList strFilesDir =
	(
		arrFolders = #()
		arrFoldersName = #()
		if strFilesDir != undefined then 
		(
			if ((strFilesDir != "") and (doesDirectoryExist strFilesDir)) then
			(
				local tempDir = GetDirectories (strFilesDir + "/*")
				for i in tempDir do 
				(
					append arrFolders (substring i 1 (i.count-1))
				)
				qsort arrFolders fnPseudoNaturalSort
				
				if arrFolders[1] != undefined then
				(
					for c in arrFolders do  --è·å–æ–‡ä»¶å¤¹åå­—,åé¢åˆ‡æ¢è„šæœ¬ç±»åˆ«å’Œåˆ—è¡¨ä¼šç”¨åˆ°
					(
						append arrFoldersName ("ğŸ“‚ " + (getFilenameFile c))
					)
				)
				rolBsBackupManager.mlbBackupFolders.items = arrFoldersName
				rolBsBackupManager.edtBackupPath.text = strFilesDir
			)
		)
		else messagebox "-------------------------------------\r\n\r\næ–‡ä»¶å¤¹å¯èƒ½å·²ä¸å­˜åœ¨\r\n\r\nè¯·å°è¯•åˆ·æ–°\r\n\r\n-------------------------------------"
		lblCountFolder.text = "å¤‡ä»½åº“ï¼š" + arrFolders.count as string + " ä¸ª"
	)

	fn fnRefreshFilesList type:".max" =
	(
		-- mlbBackupFiles.selection = 0
		idFolderSelected = 0
		nameFolderSelected = ""
		arrBsBackupFiles = #()
		arrFilesName = #()
		tarFolder = undefined
		if (mlbBackupFolders.selection as array).count == 1 then
		(
			idFolderSelected = (mlbBackupFolders.selection as array)[1]
			nameFolderSelected = (mlbBackupFolders.items)[idFolderSelected]
			nameFolderSelected = (substituteString nameFolderSelected "ğŸ“‚ " "")
			tarFolder = (edtBackupPath.text + "\\" + nameFolderSelected + ".backup\\")
		)
		else 
		(
			tarFolder = ((getdir #autoback) + "\\")
		)
		if tarFolder != undefined then 
		(
			if (doesDirectoryExist tarFolder) then
			(
				(arrBsBackupFiles = getFiles (tarFolder + "\\*" + type))
				qsort arrBsBackupFiles fnPseudoNaturalSort
				if ckbReverse.state == true then 
				(
					tempArray = for i = arrBsBackupFiles.count to 1 by -1 collect arrBsBackupFiles[i]
					arrBsBackupFiles = for i in tempArray collect i
				)
				if arrBsBackupFiles[1] != undefined then
				(
					for c in arrBsBackupFiles do  --è·å–æ–‡ä»¶åå­—,åé¢åˆ‡æ¢è„šæœ¬ç±»åˆ«å’Œåˆ—è¡¨ä¼šç”¨åˆ°
					(
						(append arrFilesName ("ğŸ“„ " + (getFilenameFile c) + type))
					)
				)
				rolBsBackupManager.mlbBackupFiles.items = arrFilesName
			)
		)
		else messagebox "-------------------------------------\r\n\r\næ–‡ä»¶ç‰ˆæœ¬å¯èƒ½å·²ä¸å­˜åœ¨\r\n\r\nè¯·å°è¯•åˆ·æ–°\r\n\r\n-------------------------------------"
		lblCountFiles.text = "å·²å¤‡ä»½ç‰ˆæœ¬ï¼š" + arrBsBackupFiles.count as string + " ä¸ª"
	)

	fn fnJudgeFolderSelected =
	(
		idSelected = 0
		if maxfilename != "" then
		(
			if not (matchpattern (maxFilePath + maxFileName) pattern:("*.backup\\*.max")) then 
			(
				arrFolder = (rolBsBackupManager.mlbBackupFolders.items as array)
				for i = 1 to arrFolder.count do
				(
					if (matchpattern arrFolder[i] pattern:("ğŸ“‚ " + (getfilenamefile maxfilename))) then idSelected = i
				)
				rolBsBackupManager.edtInputFilter.text = (getfilenamefile maxfilename)
				try(rolBsBackupManager.mlbBackupFiles.selection = 0)catch()
			)
			else 
			(
				fileName = (getfilenamefile maxfilename)
				arrNameFilter = (filterString fileName "_")
				strCountNum = arrNameFilter[arrNameFilter.count]
				tarName = substring fileName 1 (fileName.count - strCountNum.count - 1)
				arrFolder = (rolBsBackupManager.mlbBackupFolders.items as array)
				for i = 1 to arrFolder.count do
				(
					if (matchpattern arrFolder[i] pattern:("ğŸ“‚ " + tarName)) then idSelected = i
				)
				rolBsBackupManager.edtInputFilter.text = tarName
				try(rolBsBackupManager.mlbBackupFiles.selection = (finditem (rolBsBackupManager.mlbBackupFiles.items as array) ("ğŸ“„ " + maxfilename)))catch()
			)
			try(rolBsBackupManager.mlbBackupFolders.selection = idSelected)catch()
		)
	)

	fn fnRefreshBackupAll =
	(
		fnRefreshFolderList iniBackupPath
		fnJudgeFolderSelected()
		fnRefreshFilesList()
		fnJudgeFolderSelected()
	)

	on rolBsBackupManager open do 
	(
		callbacks.removeScripts #filePostOpen id:#BsBackupManager
		callbacks.removeScripts #filePostSave id:#BsBackupManager
		callbacks.addScript #filePostOpen "rolBsBackupManager.fnRefreshBackupAll()" id:#BsBackupManager
		callbacks.addScript #filePostSave "rolBsBackupManager.fnRefreshBackupAll()" id:#BsBackupManager
		btnRefreshFolder.images  = #("UVWUnwrapModes_16i.bmp","UVWUnwrapModes_16i.bmp",28,3,3,4,4,false,false)
		btnOpenCurrentDir.images = #("bip_general_i.bmp","bip_general_i.bmp",30,5,5,6,6,false,true)
		-- btnPrevFolder.images     = #("MergeAnim_24i.bmp","MergeAnim_24i.bmp",4,1,1,1,1,false,true)
		edtBackupPath.text = iniBackupPath
		fnRefreshBackupAll()
	)

	on rolBsBackupManager close do 
	(
		iniPosBackupManager = (GetDialogPos rolBsBackupManager)
		stSetConfigAll.fnSetConfigTrackBarTools()
		callbacks.removeScripts #filePostOpen id:#BsBackupManager
		callbacks.removeScripts #filePostSave id:#BsBackupManager
	)

	on btnClose pressed do 
	(
		try (destroydialog rolBsBackupManager) catch()
	)

	on rolBsBackupManager lbuttondown posMou do
	(
		posMouMove = posMou
		switchMouState = on
	)

	on rolBsBackupManager lbuttonup posMou do
	(
		switchMouState = off
		iniPosBackupManager = (GetDialogPos rolBsBackupManager)
		stSetConfigAll.fnSetConfigTrackBarTools()
	)

	on rolBsBackupManager mbuttondown pos do 
	(
		try (destroydialog rolBsBackupManager) catch()
	)

	on rolBsBackupManager mouseMove pos do
	(
		if switchMouState == on then
		(
			SetDialogPos rolBsBackupManager (mouse.screenpos - posMouMove)
		)
	)

	-- on btnPrevFolder pressed do 
	-- (
	-- 	edtBackupPath.text = (fnGetLastFolder edtBackupPath.text)
	-- 	fnRefreshBackupAll()
	-- )

	on btnRefreshFolder pressed do 
	(
		rolBsBackupManager.fnRefreshBackupAll()
	)

	on btnRefreshFolder rightclick do 
	(
		mlbBackupFolders.selection = 0
		rolBsBackupManager.fnRefreshFilesList()
	)

	on btnOpenCurrentDir pressed do 
	(
		try(shellLaunch iniBackupPath "")catch()
	)

	on mlbBackupFolders selected arg do
	(
		if (rolBsBackupManager.mlbBackupFolders.selection as array).count == 1 then
		(
			rolBsBackupManager.fnRefreshFilesList()
			selItems = mlbBackupFolders.items[(mlbBackupFolders.selection as array)[1]]
			edtInputFilter.text = (substring selItems 3 selItems.count)
		)
	)

	on mlbBackupFiles doubleClicked id do
	(
		tarFile = arrBsBackupFiles[id]
		if (doesfileexist tarFile) then
		(
			if CheckForSave() then (loadMaxFile tarFile useFileUnits:true)
		)
	)

	on edtInputFilter changed textFolder do
	(
		if textFolder != "" then 
		(
			local arrID = #()
			for i in (rolBsBackupManager.mlbBackupFolders.items as array) do 
			(
				if (matchpattern i pattern:("*" + textFolder + "*")) then
				(
					append arrID (finditem (rolBsBackupManager.mlbBackupFolders.items as array) i)
				)
			)
			mlbBackupFolders.selection = arrID
		)
		else 
		(
			mlbBackupFolders.selection = 0
		)
		rolBsBackupManager.fnRefreshFilesList()
	)

	on mlbBackupFiles rightclick do
	(
		popupMenu menuBsFiles pos:mouse.screenpos
	)

	on mlbBackupFolders rightclick do
	(
		popupMenu menuBsFolder pos:mouse.screenpos
	)

	on ckbReverse changed state do 
	(
		rolBsBackupManager.fnRefreshFilesList()
	)
)