-- è¯•éªŒåŠŸèƒ½å¯ä»¥åŠ å…¥å¿«é€Ÿåˆ›å»ºæ–¹å—æ­¦å™¨ï¼Œåˆ€å‰‘ï¼Œé”¤ï¼Œé•¿æªï¼Œæ‰‹æªç­‰~~
-- é›†æˆ RefBones å·¥å…·ï¼ŒWireColorPro å·¥å…·ç­‰

try(destroyDialog rolBsBoxMan )catch()

global rolBsBoxMan
global offsetBsBoxMan = [0,0]
global dragStateBsBoxMan = off

(
	local LastSubRollout = 1
	
	rollout rolBsBoxMan "" width:260 height:430
	(
		local dotColor = dotnetclass "System.Drawing.Color"

		button btnClose "X" pos:[rolBsBoxMan.width - 20,0] height:16 width:20
		label lblTitle "BsBoxMan_v1.1" pos:[5,3]
		dotNetControl tabBoxMan "System.Windows.Forms.TabControl" height:30 width:260 pos:[8,25] align:#center
		
		groupBox gpbBox "" pos:[5,55] width:250 height:110
		
		checkbutton chkCreateBodyBox ">> æ ¹æ®æ‰€é€‰ç‰©ä½“ æ¸²æŸ“æ¡† åˆ›å»º Body Box <<" pos:[10,70] height:25 width:240 border:false checked:true 
		radiobuttons rdoColor labels:#("éšæœºé¢œè‰²", "ç»Ÿä¸€å•è‰²") columns:2 align:#center pos:[20,105]
		colorpicker rtcp "" width:70 height:20 color:[38,130,118] pos:[170,103]
		checkbox ckbFreeze "Box æ˜¯å¦å†»ç»“" pos:[20,135] width:100 height:20 color:[152, 227, 213] checked:true
		button btnClearBox "ä¸€é”®æ¸…é™¤ Body Box" pos:[120,135] width:130 height:20 border:true
			
		groupBox gbpLWH "" pos:[5,170] width:250 height:180
		
		checkbutton ckbSync "ç­‰æ¯”è°ƒèŠ‚" pos:[175,310] height:30 width:75 tooltip:"åŒæ—¶è°ƒèŠ‚é•¿å®½é«˜" border:false checked:true 
		checkbutton ckbReset "é‡ç½®å¤§å°" pos:[110,310] height:30 width:60 tooltip:"é‡ç½®é•¿å®½é«˜" border:false checked:true 
		radiobuttons rdoReduce labels:#("æ”¾å¤§", "ç¼©å°") columns:2 pos:[15,318] height:30 width:50 border:true Selection:1
		slider sldLenthValue ""  pos:[50,185] align:#center range:[1,20,1] \
		type:#integer width:170 ticks:50
		slider sldWidthValue ""  pos:[50,225] align:#center range:[1,20,1] \
		type:#integer width:170 ticks:50 
		slider sldHeightValue ""  pos:[50,265] align:#center range:[1,20,1] \
		type:#integer width:170 ticks:50
		label lblLenth "Xï¼š" pos:[15,195] width:30
		label lblWidth "Yï¼š" pos:[15,235] width:30
		label lblHeight "Zï¼š" pos:[15,275] width:30
		label lblLenthValue "x " pos:[215,195] width:30
		label lblWidthValue "x " pos:[215,235] width:30
		label lblHeightValue "x " pos:[215,275] width:30
		
		groupBox gpbDisplayUnit "Display Unit" pos:[5,355] width:105 height:50
		dropdownlist ddlMetric "" align:#left selection:2 height:15 width:95 pos:[10,375] \
		items:#("Millimeters","Centimeters","Meters","Kilometers")

		groupBox gpbSystemUnit "System Unit" pos:[115,355] width:105 height:50
		dropdownlist ddlSystemUnit "" align:#left selection:5 height:15 width:95 pos:[120,375] \
		items:#("Inches","Feet","Miles","Millimeters","Centimeters","Meters","Kilometers")

		button btnGetUnit "åˆ·æ–°" tooltip:"è·å–å½“å‰ Unit (å•ä½) çŠ¶æ€" pos:[225,361] width:30 height:44

		dotnetcontrol lblTips "Label" text:"2023.2 [ Bullet.S ] âœ¨        åˆ›å»ºä¸“å±ç»ƒä¹ æ–¹å—äºº" pos:[0,413] width:260 height:16

--------------------------------------------------------------------------------------------------------------------------------------------

		local IDKEYWORD = "RefBone"		-- Ref Bone ì„ì„ ì•Œì•„ë³´ëŠ” ìœ ì €ë””íŒŒì¸ ì •ë³´ìš© í‚¤ì›Œë“œ ë””íŒŒì¸
		local IDKEYWORDHANDLE = "RefBoneHandle"		-- Ref Bone ì„ì„ ì•Œì•„ë³´ëŠ” ìœ ì €ë””íŒŒì¸ ì •ë³´ìš© í‚¤ì›Œë“œ ë””íŒŒì¸
		local IDKEYWORDTARGETHANDLE = "RefBoneTargetHandle"		-- Ref ë³¸ ì˜ íƒ€ê²Ÿì— Ref ë³¸ì˜ í•¸ë“¤ ë²ˆí˜¸ë¥¼ ê¸°ë¡
		local NAMEPREFIX = "Ref_"
		
		checkbutton chkProcess "ç”Ÿæˆå‚è€ƒéª¨éª¼" width:100 pos:[10,70] visible:false border:false checked:true 
		button uiBtnSelect "é€‰ä¸­" width:50 visible:false pos:[120,70]
		button uiBtnClearAll "æ¸…é™¤å‚è€ƒ" width:70 visible:false pos:[180,70]
		spinner uiSpnBoneWidth "éª¨éª¼ç¼©æ”¾æ¯” %" pos:[50, 105] tooltip:"é€‰æ‹©å¹¶è°ƒæ•´åˆ›å»ºçš„éª¨éª¼çš„å¤§å°ã€‚" range:[1, 500, 100] type:#integer width:100  visible:false
		colorPicker uiColorWireColor "é¢œè‰²ï¼š" color:yellow pos:[165, 102] toolTip:"é€‰æ‹©å¹¶æ›´æ”¹é¢œè‰²ã€‚"  visible:false height:20

		--checkbox <name> [<caption>] [checked:<boolean>] [triState:<integer>][tooltip:<string>
		checkbox uiChkOptPos "Position" checked:true  visible:false pos:[15,135]
		checkbox uiChkOptRot "Rotation" checked:true visible:false pos:[100,135]
		checkbox uiChkOptScale "Scale" checked:true visible:false pos:[185,135]
		
		checkbox uiChkOptInheritScale "ç»§æ‰¿ç¼©æ”¾" pos:[15,160] checked:false toolTip:"é»˜è®¤ï¼šå…³é—­\r\n\r\né™¤éæœ‰ç‰¹æ®Šæƒ…å†µï¼Œå¦åˆ™å»ºè®®ä¸ç»§æ‰¿"  visible:false
		checkbox uiChkKeepParent "ä¿æŒçˆ¶å­å±‚çº§å…³ç³»" pos:[100,160] checked:false  visible:false
		
		/*
		Set Scalable Biped ë²„íŠ¼ì€ ì²™ì¶”ì™€ ì‡„ê³¨ì˜ ìœ„ì¹˜ë¥¼ ë³´ì •í•´ì£¼ëŠ” ì„œë¸Œì• ë‹˜ê¹Œì§€ ì¶”ê°€í•˜ëŠ” ì‘ì—…ì´ í•„ìš”í•´ì„œ ë§Œë“¤ë‹¤ ë§ê³  í™€ë“œ --> ì„œë¸Œì• ë‹˜ ìœ„ì¹˜ ë³´ì •ì„ í•´ë³´ë ¤ í—€ëŠ”ë°, ë‹¨ìˆœ ìŠ¤ì¼€ì¼ì—ì„œ ì–´ê¸‹ë‚˜ëŠ” í¬ì§€ì…˜ì´ ê·œì¹™ì´ ì—†ë‹¤. (ìŠ¤ì¼€ì¼ì„ í‚¤ë¥¼ ê±¸ì–´ë†“ê³  ë³´ë©´ í™•ì‹¤í•¨)
		button uiBtnSetScalableBiped "Set Scalable Biped" tooltip:"Scaleì´ ê°€ëŠ¥í•œ ë°”ì´íŒ¨ë“œë¡œ ë³€ê²½í•˜ê³  Scaleì„ ì¡°ì •í•˜ëŠ” í—¬í¼ë¥¼ ì¶”ê°€í•©ë‹ˆë‹¤."
		*/
		
		-- ë”ë¯¸ ìƒì„±í•˜ëŠ” ìŠ¤í¬ë¦½íŠ¸ --
		spinner uiDummySize "Point å¤§å°ï¼š" pos:[150, 203] tooltip:"åˆ›å»ºè¾…åŠ©ç‚¹çš„å¤§å°" range:[0.001, 100, 0.1] type:#float align:#right width:95 visible:false
		checkbutton uiBtnDummy "ç”Ÿæˆè¾…åŠ© Point" pos:[10, 200] width:90 visible:false border:false checked:true 
		button uiBtnHelp "RefBones å…³äºï¼ŒåŸä½œè€…" visible:false width:240 pos:[10, 240]

-----------------------------------------------------------------------------------------------------------------------------------------

		groupBox gpbType "" pos:[5,55] width:250 height:145 visible:false

		radiobuttons radObject "ç±»å‹" pos:[20,70] labels:#( "å‡ ä½•ä½“", "å½¢çŠ¶", "æ‰€æœ‰ç»„", "æ‰€æœ‰ç‰©ä½“", "é€‰æ‹©ç‰©ä½“" )  visible:false
		radiobuttons radColor "é…è‰²" pos:[130,70] columns:2 labels:#( "ç²‰å½©", "ç°ç™½", "æ¯’å½©" , "é»åœŸ" , "æ·¡å½©", "æ·±æ²‰" , "è§å½©", "éšæœº")  visible:false
		checkButton chkIsInstance "ç›¸å…³å®ä¾‹" pos:[20,170] height:20 width:80 tooltip:"å¯ç”¨åï¼Œç›¸å…³å®ä¾‹\r\n(å¦‚å¤åˆ¶å‡ºçš„Instance)\r\nä¹Ÿä¼šä¿æŒä¸€è‡´çš„é¢œè‰²ã€‚" height:  30 align:#left  visible:false
		checkbutton btnRandom ">> éšæœºé¢œè‰² <<" pos:[120,160] height:30 width:125 visible:false border:false checked:true 

		groupBox gpbGradient "æ¸å˜" pos:[5,210] width:170 height:45 visible:false
		colorpicker cpGradient1 pos:[15,230] color:  [58, 122, 205] width:70 height:15 align:#left  visible:false
		colorpicker cpGradient2 pos:[95,230] color:  [139, 49, 165] width:70 height:15 align:#right visible:false

		groupBox gpbRealTime "å®æ—¶" pos:[185,210] width:70 height:45 visible:false
		colorpicker cp "" pos:[195,230] width:50 height:15 color:  [119, 95, 192] align:#center visible:false

		groupBox gpbMaterial "æè´¨" pos:[5,260] width:250 height:60 visible:false
		radiobuttons radMaterial pos:[15,280] labels:#( "æ ‡å‡†", "ç‰©ç†" ) columns:1 visible:false
		button btnConverter "è½¬æ¢" pos:[70,280] width:50 height:30 tooltip:"Convert WireColor to Material" width:70 height:30 visible:false

		radiobuttons radErase pos:[140,280] labels:#( "é€‰æ‹©", "æ‰€æœ‰" )  columns:1 visible:false 
		Button btnErase "æ¸…é™¤" pos:[195,280] width:50 height:30 tooltip:"Assign to objects" width:70 height:30 visible:false
		hyperLink author "markulie" pos:[15,390] address:"https://markulie.github.io" color:(color 255 255 255) hovercolor:(color 255 0 0) visitedcolor:(color 255 255 255) visible:false
-----------------------------------------------------------------------------------------------------------------------------------------

		edittext edtValue "\"å¡é€šè‰²\""  pos:[10,332] width:85 usePercentageWidth:true  visible:false \
		percentageWidth:44.0 labelOnTop:false text:"100" bold:false readOnly:false --è‡ªå‘å…‰æ•°å€¼
		button btnSet "åº”ç”¨è‡ªå‘å…‰"  pos:[180,330] width:75 height:20 tooltip:"è°ƒèŠ‚é€‰ä¸­ï¼Œå¦åˆ™å…¨éƒ¨ã€‚\r\néœ€è¦å…ˆæœ‰æè´¨!\r\nä¸”éä¸€è‡´è‰²å½©æ¨¡å¼!"  visible:false
		button btnSet0 "0"  pos:[105,330] width:25 height:20 tooltip:"è°ƒèŠ‚é€‰ä¸­ï¼Œå¦åˆ™å…¨éƒ¨ã€‚"  visible:false
		button btnSet100 "100"  pos:[140,330] width:30 height:20 tooltip:"è°ƒèŠ‚é€‰ä¸­ï¼Œå¦åˆ™å…¨éƒ¨ã€‚"  visible:false
		slider sldValue ""  pos:[15,350] align:#center range:[0,100,0]  visible:false \
		type:#integer tooltip:"è°ƒèŠ‚é€‰ä¸­ï¼Œå¦åˆ™å…¨éƒ¨ã€‚" width:250

		HyperLink lnkLink "Adapter: Bullet.S" color:(color 255 255 255) hovercolor:(color 255 0 0) visitedcolor:(color 255 255 255) visible:false \
		address:"https://space.bilibili.com/2031113/channel/collectiondetail?sid=560782" pos:[165,390]

---------------------------------------------------------------------------------------------------------------------------------------------------------

		local arrCreateBox = #("æ–¹å—åŒ…è£¹",#(lblLenth,lblWidth,lblHeight,ckbReset,gpbBox,gbpLWH,\
			chkCreateBodyBox,rdoColor,rtcp,ckbFreeze,btnClearBox,ckbSync,rdoReduce,sldLenthValue,sldWidthValue,\
			sldHeightValue,gpbDisplayUnit,gpbSystemUnit,btnGetUnit,ddlSystemUnit,ddlMetric,lblLenthValue,lblWidthValue,lblHeightValue))
		local arrRefBones = #("å‚è€ƒéª¨éª¼",#(uiChkKeepParent,uiBtnClearAll,chkProcess,uiBtnSelect,uiSpnBoneWidth,uiColorWireColor,uiChkOptPos,\
			uiChkOptRot,uiChkOptScale,uiChkOptInheritScale,uiDummySize,uiBtnDummy,uiBtnHelp))
		local arrEquipTools = #("âš”ï¸ è£…å¤‡",#())
		local arrColorTools = #("ğŸ¨ è°ƒè‰²",#(gpbRealTime,gpbType,radObject,radColor,chkIsInstance,btnRandom,gpbGradient,cpGradient1,cpGradient2,\
			cp,gpbMaterial,radMaterial,btnConverter,radErase,btnErase,author,edtValue,btnSet,btnSet0,btnSet100,sldValue,lnkLink))
		local arrAllToolsTab = #(arrCreateBox,arrRefBones,arrEquipTools,arrColorTools)

		fn fnRefreshSliderValue =
		(
			rolBsBoxMan.lblLenthValue.text = "x " + (rolBsBoxMan.sldLenthValue.value as string)
			rolBsBoxMan.lblWidthValue.text = "x " + (rolBsBoxMan.sldWidthValue.value as string)
			rolBsBoxMan.lblHeightValue.text = "x " + (rolBsBoxMan.sldHeightValue.value as string)
		)
		
		function GetAlignBB obj = (		-- ì›”ë“œ ì¢Œí‘œìƒì—ì„œ ììœ ë¡­ê²Œ íšŒì „ë˜ì–´ìˆëŠ” ì˜¤ë¸Œì íŠ¸ì˜ ë°”ìš´ë”© ë°•ìŠ¤ í¬ì§€ì…˜ì„ ì›”ë“œ ì¤‘ì ìœ¼ë¡œ ìœ„ì¹˜ì™€ íšŒì „ì„ ë¦¬ì…‹í•˜ì—¬ point ê°’ ë‘ ê°œë¥¼ ë¦¬í„´í•œë‹¤. ì£¼ë¡œ ê¸¸ì´ë¥¼ ì¬ê¸° ìœ„í•œ ìš©ë„
			bb = nodeGetBoundingBox obj obj.transform		-- ë¡œì»¬ ë°”ìš´ë”© ë°•ìŠ¤ í¬ì§€ì…˜ ê°’ ë‘ ê°œê°€ ë°°ì—´ë¡œ bbì— ì €ì¥ë¨ bb[1], bb[2]
			
			if ((classof obj.baseobject) == Point) do
			(	-- í¬ì¸íŠ¸í—¬í¼ëŠ” ì ìœ¼ë¡œë§Œ ì¡´ì¬í•´ì„œ ë°”ìš´ë”©ë°•ìŠ¤ë¥¼ ê°•ì œë¡œ ë„£ì—Šì¤˜ì•¼í•œë‹¤.
				bb[1].x -= obj.baseobject.size * 0.5
				bb[1].y -= obj.baseobject.size * 0.5
				bb[2].x += obj.baseobject.size * 0.5
				bb[2].y += obj.baseobject.size * 0.5
			)
			return bb
		)
		
		function GetWidthX obj = (		-- ì„ íƒëœ ì˜¤ë¸Œì íŠ¸ì˜ ë¡œì»¬ ë°”ìš´ë”© ë°•ìŠ¤ ê¸°ì¤€ìœ¼ë¡œ xì¶• ë°©í–¥ì˜ ë‘ê»˜ë¥¼ ì•Œì•„ë‚¸ë‹¤. (ê¸¸ì´ê°€ ì•„ë‹Œ ë‘ê»˜ --> ë³¸ì˜ ë‘ê»˜ ê°’ì— ì‚¬ìš©í•˜ê¸° ìœ„í•¨)
			bb = GetAlignBB obj		-- ì›”ë“œ ì¤‘ì ìœ¼ë¡œ íšŒì „ê³¼ ìœ„ì¹˜ê°€ ë¦¬ì…‹ëœ ë°”ìš´ë”© ë°•ìŠ¤ ìƒì„±
			return (((abs (bb[2].y - bb[1].y)) + (abs (bb[2].z - bb[1].z))) / 2.0)		-- zì¶• ë‘ê»˜ì™€ yì¶• ë‘ê»˜ì˜ í‰ê· ê°’ì„ êµ¬í•œë‹¤.
		)
		
		function CheckWorkingBone obj = (		-- ì–´ë–¤ ì‘ì—…ì„ í• ì§€ ê²€ì‚¬. "IGNORE", "CREATE", "MODIFY" ì´ë ‡ê²Œ ì„¸ ê°€ì§€ stringì„ ë¦¬í„´í•œë‹¤.
			if (getUserProp obj "RefBone") == true do (return "MODIFY")
			
			tFlag = false		-- ì„ì‹œ í”Œë˜ê·¸ ìƒì„±
			
			-------------------- ê²€ì‚¬ ì‹œì‘
			if (classof obj.baseobject == Biped_Object) do (tFlag = true)		-- ë°”ì´íŒ¨ë“œì¸ ê²½ìš°, ëª¨ë””íŒŒì´ì–´ê°€ ì ìš©ëœ ë°”ì´íŒ¨ë“œë‚˜ ë³¸ì—ëŠ” baseobject ì²˜ë¦¬í•´ì¤˜ì•¼í•¨
			if (obj.BoneEnable == true) do (tFlag = true)		-- ë³¸ì¸ ê²½ìš°
			if (classof obj.baseobject == Point) do (tFlag = true)
			if (classof obj.baseobject == Dummy) do (tFlag = true)
			
			if ((getUserProp obj IDKEYWORD) == true) do (tFlag = false)		-- ìµœì¢…ì ìœ¼ë¡œ RefBone ì´ë©´ ë¬´ì¡°ê±´ false ì²˜ë¦¬
			-------------------- ê²€ì‚¬ ë
			
			if tFlag == true then (return "CREATE") else (return "IGNORE")
		)

		-- childrenì´ Arrayë¡œ ë°”ë¡œ ë³€í™˜ì´ ì•ˆë¼ì„œ ë§Œë“  í•¨ìˆ˜. ë°°ì—´ì„ ë¦¬í„´
		function ChildrenToArray childrenArray = (
			local returnArray = #()
			for o in childrenArray do (
				append returnArray o
			)
			return returnArray
		)
		
		function FindLookAtPos obj = (		-- ë°”ì´íŒ¨ë“œì˜ ëª©ì²˜ëŸ¼ ìì‹ì´ ì—¬ëŸ¿ ìˆëŠ” ë³¸ì€ ë¨¸ë¦¬ë¥¼ ë°”ë¼ë³´ë©´ì„œ RefBone ì´ ìƒì„±ë˜ì–´ì•¼ í•œë‹¤. ì´ëŸ° ê²½ìš° ë˜‘ë˜‘í•˜ê²Œ ë°”ë¼ë³¼ ë³¸ì„ ì°¾ì•„ì£¼ëŠ” í•¨ìˆ˜. ë°”ë¼ë³¼ ìœ„ì¹˜ë¥¼ ë¦¬í„´í•œë‹¤.
		-- ì‘ë™ ë°©ì‹ì€, ë¶€ëª¨ ë³¸ ìœ„ì¹˜ì—ì„œ ìì‹ë³¸ ìœ„ì¹˜ì˜ ê±°ë¦¬ë¥¼ ëª¨ë‘ ì•Œì•„ë‚´ì„œ, ë¶€ëª¨ë³¸ìœ¼ë¡œë¶€í„° xì¶•ìœ¼ë¡œ ì´ë™ì‹œì¼°ë”ë‹ˆ ê±°ë¦¬ê°€ ì˜¤íˆë ¤ ë©€ì–´ì§€ë©´ ë°”ë¼ë³´ëŠ” ë³¸ì´ ì•„ë‹ˆë¼ëŠ” ë°©ì‹ìœ¼ë¡œ ê²€ì¶œ
		-- ê²€ì‚¬ì¤‘ì¸ obj ê°€ ì¼ë°˜ ë³¸ì¼ ê²½ìš° .length ê°’ì„ í™œìš©í•œë‹¤.
		-- ìì‹ì´ ì—†ëŠ” ë°”ì´íŒ¨ë“œ ì˜¤ë¸Œì íŠ¸ë‚˜ ì¼ë°˜ ì˜¤ë¸Œì íŠ¸ì˜ BoneOn ì¸ ê²½ìš°ì—ëŠ” ë°”ìš´ë”© ë°•ìŠ¤ ì •ë³´ë¥¼ ê¸¸ì´ë¡œ í™œìš©í•œë‹¤.
			
			tMatrix = obj.transform		-- ìŠ¤ì¼€ì¼ì´ ë¦¬ì…‹ëœ ê¸°ì¤€ìœ¼ë¡œ tLength ê³„ì‚°ì„ í•´ì•¼í•¨
			tMatrix.scale = [1, 1, 1]
			tMatrix.rotation = obj.transform.rotation
			tMatrix.position = obj.transform.position
			
			tLength = 1.0		-- ê³„ì‚°í•  ê¸¸ì´ ë³€ìˆ˜ ì…‹íŒ…. ë””í´íŠ¸ê°’ì€ 1.0
			
			if obj.children.count == 0 then (		-- ìì‹ì´ í•˜ë‚˜ë„ ì—†ëŠ” ê²½ìš° ë³¸ì˜ length ê°’ì„ ì´ìš©í•˜ê±°ë‚˜ ë¡œì»¬ ë°”ìš´ë”© ë°•ìŠ¤ ì¤‘ì‹¬ìœ¼ë¡œë¶€í„° ì–‘ì˜ xì¶• ê¸¸ì´ë¥¼ ì´ìš©í•œë‹¤.
				tLength = case (classof obj.baseobject) of
				(
				BoneGeometry: obj.baseobject.length * obj.transform.scale.x
				Biped_Object: (biped.getTransform obj #scale).x
				default: (GetAlignBB obj)[2].x
				)
				/*
				if ((classof obj.baseobject) == BoneGeometry) then (
					tLength = obj.baseobject.length * obj.transform.scale.x
				)
				else (
					tLength = (GetAlignBB obj)[2].x
				)
				*/
			)
			else		-- ìì‹ì´ í•˜ë‚˜ ì´ìƒì¸ ê²½ìš°
			(
				local targetNode
				local beforeDist = -1.0
				local childrenObjs = #() -- ìì‹ë“¤ì´ ì—¬ëŸ¿ì¼ ë•Œ ë°”ì´íŒ¨ë“œê°€ í•˜ë‚˜ë¼ë„ ìˆìœ¼ë©´ ë°”ì´íŒ¨ë“œë§Œ ëŒ€ìƒìœ¼ë¡œ ìì‹ì„ ë‹¤ì‹œ ì„ ë³„í•˜ê¸° ìœ„í•œ ë°°ì—´
				
				for o in obj.children do
				(
					if (classof o.baseobject) == Biped_Object do
					(
						append childrenObjs o -- ë°”ì´íŒ¨ë“œë¡œ íŒëª…ë˜ë©´ ë°°ì—´ì— ì¶”ê°€
					)

				)
				
				if childrenObjs.count == 0 do
				(
					-- ë°”ì´íŒ¨ë“œê°€ í•˜ë‚˜ë„ ë°œê²¬ë˜ì§€ ì•Šìœ¼ë©´ childrenObjsì— ì›ë˜ ìì‹ë“¤ì„ ë‹¤ì‹œ ì ìš©
					childrenObjs = ChildrenToArray obj.children
				)

				for o in childrenObjs do
				(
					vDist = distance obj.transform.pos o.transform.pos		-- ê¸°ì¤€ ì˜¤ë¸Œì íŠ¸ë¡œë¶€í„° ìì‹ ì˜¤ë¸Œì íŠ¸ì˜ ê±°ë¦¬ë¥¼ ì¸¡ì •
					if vDist > 0.001 do		-- ê°™ì€ ìœ„ì¹˜ì— ìˆëŠ” ë³¸ì€ ì²˜ë¦¬ ëŒ€ìƒì—ì„œ ì œì™¸í•œë‹¤. ì œì™¸ ê²°ê³¼ ìì‹ì´ í•˜ë‚˜ë„ ì—†ì„ ìˆ˜ ìˆëŠ”ë° ì´ë•ŒëŠ” tLength ëŠ” ê¸°ë³¸ê°’ì¸ 1.0ì´ ëœë‹¤.
					(
						xPos = ((transMatrix [vDist, 0, 0]) * tMatrix).pos		-- ìì‹ì˜¤ë¸Œì íŠ¸ ê±°ë¦¬ë§Œí¼ ê¸°ì¤€ ì˜¤ë¸Œì íŠ¸ì˜ xì¶•ìœ¼ë¡œ ì´ë™í•œ ìœ„ì¹˜
						endDist = distance xPos o.transform.pos		-- ì´ë™í•œ ìœ„ì¹˜ë¡œë¶€í„° ìì‹ ì˜¤ë¸Œì íŠ¸ê¹Œì§€ì˜ ê±°ë¦¬ (ì´ ê±°ë¦¬ê°€ ì§§ì„ ìˆ˜ë¡ ì‹¤ì œ ëª©í‘œ ìì‹ì„. xì¶•ì— ì •ë ¬ë˜ì–´ìˆì„ ìˆ˜ë¡ ì´ ê±°ë¦¬ê°€ ì§§ë‹¤. xì¶•ì— ë²—ì–´ë‚˜ë©´ ì´ ê±°ë¦¬ê°€ ì»¤ì§„ë‹¤.)
						
						if beforeDist == -1.0 OR endDist <= beforeDist do		-- í˜„ì¬ oì˜ endDist ê°€ ì´ì „ë³´ë‹¤ ë” ì§§ìœ¼ë©´ oë¥¼ íƒ€ê²Ÿ ë…¸ë“œë¡œ ì§€ì • í›„ beforeDist ì—…ë°ì´íŠ¸
						(
							targetNode = o
							beforeDist = endDist
							tLength = (cos ((90 - acos ((endDist * 0.5) / vDist)) * 2)) * vDist		-- íƒ€ê²Ÿì´ xì¶• ì¤‘ì‹¬ì—ì„œ ì–´ê¸‹ë‚˜ìˆì„ ê²½ìš°ì—ëŠ” ì‚¼ê°í•¨ìˆ˜ë¡œ ê³„ì‚°í•´ì•¼í•¨
						)
					)
				)
			)
			
			return ((transMatrix [tLength, 0, 0]) * tMatrix).pos		-- objë¡œë¶€í„° tLength ë§Œí¼ ì´ë™í•œ íŠ¸ëœìŠ¤í¼ì˜ pos ë¦¬í„´
		)
		
		function CreateBone pStart pEnd vUp vSize = (		-- ì‹œì‘ì , ëì , ì—…ë²¡í„°, ë‘ê»˜ ì…ë ¥ë°›ê³  ë³¸ ìƒì„±. ë‚˜ë¨¸ì§€ ëª¨ë“  ë³¸ ì§€ì˜¤ë©”íŠ¸ë¦¬ ë³€ìˆ˜ë“¤ì€ ë””í´íŠ¸ë¡œ ê°•ì œ ì…‹íŒ…
			tBone = BoneSys.createBone pStart pEnd vUp
			tBone.width = vSize
			tBone.height = vSize
			tBone.taper = 90.0
			-- tBone.length ì´ê²ƒì€ ìë™ ê²°ì •ë¨
			tBone.sidefins = false
			tBone.sidefinssize = vSize * 0.5
			tBone.sidefinsstarttaper 
			tBone.sidefinsendtaper 
			tBone.frontfin = false
			tBone.frontfinsize = vSize * 0.5
			tBone.frontfinstarttaper 
			tBone.frontfinendtaper 
			tBone.backfin = false
			tBone.backfinsize = vSize * 0.5
			tBone.backfinstarttaper = 10.0
			tBone.backfinendtaper = 10.0
			tBone.genmap = false
			
			tBone.boneScaleType=#none
			tBone.boneAutoAlign=false
			
			return tBone
		)
		
		function SetRefBoneAttr targetObj newObj = (		-- Ref Bone ì†ì„±ê³¼ ì»¬ëŸ¬ë¥¼ ì ìš©í•œë‹¤
			newObj.WireColor = uiColorWireColor.color
			setUserProp newObj IDKEYWORD "true"
			setUserProp newObj IDKEYWORDHANDLE targetObj.inode.handle
			setUserProp targetObj IDKEYWORDTARGETHANDLE newObj.inode.handle
			newObj.name = NAMEPREFIX + targetObj.name
		)
		
		function SetRefController targetObj newObj = (		-- íƒ€ê²Ÿ ì˜¤ë¸Œì íŠ¸ë¥¼ ë”°ë¼í•˜ë„ë¡ ìŠ¤í¬ë¦½íŠ¸ ì»¨íŠ¸ë¡¤ëŸ¬ë¡œ ë³€ê²½. ë³€ê²½ ë‹¹ì‹œ ë³¸ì˜ ìƒëŒ€ì ì¸ transform ìœ ì§€ëŠ” ë³´ë¥˜.
			-- ì¼ë‹¨ ì»¨íŠ¸ë¡¤ëŸ¬ ë¦¬ì…‹
			newObj.pos.controller = Position_XYZ ()
			newObj.rotation.controller = Euler_XYZ ()
			newObj.scale.controller = bezier_scale ()

			newObj.scale = targetObj.transform.scale
			newObj.rotation = targetObj.transform.rotation
			newObj.pos = targetObj.transform.pos
			
			if (uiChkOptPos.state == true) do (
				newObj.pos.controller = Position_Constraint ()
				newObj.pos.controller.appendTarget targetObj 50.0
			)

			if (uiChkOptRot.state == true) do (
				newObj.rotation.controller = Orientation_Constraint ()
				newObj.rotation.controller.appendTarget targetObj 50.0
			)

			if (uiChkOptScale.state == true) do (
				newObj.scale.controller = scale_script ()
				newObj.scale.controller.AddNode "TG" targetObj
				newObj.scale.controller.SetExpression "tScale = try (TG.transform.scale * TG.inode.stretchTM.scale) catch ([1, 1, 1])
	if abs (tScale.x - 1.0) < 0.00001 do (tScale.x = 1.0)
	if abs (tScale.y - 1.0) < 0.00001 do (tScale.y = 1.0)
	if abs (tScale.z - 1.0) < 0.00001 do (tScale.z = 1.0)
	tScale"
			)

			-- Scale ìƒì†
			if ( uiChkOptInheritScale.state == false ) then (
				setInheritanceFlags newObj #{1, 2, 3, 4, 5, 6}		-- scale ìƒì† ê´€ë ¨ ë¬¸ì œë¥¼ ë§‰ê¸° ìœ„í•´ inherit ë¥¼ êº¼ì¤Œ
			)
			else (
				setInheritanceFlags newObj #{1, 2, 3, 4, 5, 6, 7, 8, 9}		-- scale ì„ ê°•ì œë¡œ ìƒì†í•˜ë ¤ë©´ ì¼œì¤Œ
			)
		)

		-- ë ˆí”„ë³¸ì´ ì°¸ì¡°í•˜ëŠ” ì›ë³¸ (ë°”ì´íŒ¨ë“œ ë“±)ì„ ë¦¬í„´í•œë‹¤.
		function GetRefOrigin ref = (
			local handle = getUserProp ref IDKEYWORDHANDLE
			return (maxOps.getNodeByHandle handle)
		)

		-- ì›ë³¸ì´ ì°¸ì¡°í•˜ëŠ” Refë³¸ì„ ë¦¬í„´í•œë‹¤. ì›ë³¸ì— Refë³¸ ê¸°ë¡ì´ ì—†ê±°ë‚˜ ì°¾ì„ ìˆ˜ ì—†ìœ¼ë©´ undefined ë¦¬í„´.
		function GetRef origin = (
			local refBone
			try (refBone = maxOps.getNodeByHandle (getUserProp origin IDKEYWORDTARGETHANDLE)) catch (
				refBone = undefined
			)

			return refBone
		)
		
		function CheckIfExist obj = (		-- í˜„ì¬ ì²˜ë¦¬í•˜ë ¤ê³  í•˜ëŠ” ì˜¤ë¸Œì íŠ¸ì— ì´ë¯¸ Ref Bone ì´ ìƒì„±ë˜ì–´ìˆëŠ”ì§€ë¥¼ ì¡°ì‚¬. Ref Bone íˆ´ë¡œ ìƒì„±ëœ ë³¸ì—ëŠ” ë¬´ì¡°ê±´ IDKEYWORDHANDLE = "RefBoneHandle" ì˜ ìœ ì € í”„ë¡œí¼í‹°ë¡œ ì›ë³¸ ë³¸ì˜ í•¸ë“¤ ë²ˆí˜¸ê°€ ìˆê¸°ë•Œë¬¸ì— ì´ë¥¼ ì¡°ì‚¬í•¨)
			if obj == undefined do return undefined		-- ë¶€ëª¨ì˜ Ref Bone ì„ ì°¾ëŠ” ê³¼ì •ì—ì„œ undefined ê°€ obj ë¡œ ì…ë ¥ë  ìˆ˜ ìˆì–´ì„œ ë°©ì–´ì½”ë“œ
			
			local rNode
			rNode = GetRef obj -- ì¼ë‹¨ í˜„ì¬ ì˜¤ë¸Œì íŠ¸ì— Refë³¸ ê¸°ë¡ì´ ìˆëŠ”ì§€ ì°¾ì•„ë³¸ë‹¤.
			if (rNode != undefined) do (return rNode) -- ê¸°ë¡ì´ ìˆìœ¼ë©´ ê·¸ ì˜¤ë¸Œì íŠ¸ë¥¼ ë¦¬í„´

			-- ê¸°ë¡ì´ ì—†ìœ¼ë©´ ì „ì²´ ì˜¤ë¸Œì íŠ¸ ì•ˆì—ì„œ ë‹¤ ì°¾ì•„ë³¸ë‹¤.
			for o in objects do (
				if (obj.inode.handle == getUserProp o IDKEYWORDHANDLE) do (rNode = o)
			)
			return rNode		-- Ref Bone ì„ ë¦¬í„´í•œë‹¤. ëª»ì°¾ìœ¼ë©´ undefined ë¦¬í„´
		)
		
		function AutoParent targetObj newObj = (		-- ìƒˆë¡œ ìƒì„±ëœ Ref Bone ì˜ ë¶€ëª¨ë¥¼ ìë™ ì§€ì •í•˜ë„ë¡ í•˜ëŠ” í•¨ìˆ˜
			if targetObj.parent == undefined do return ()		-- íƒ€ê²Ÿ ì˜¤ë¸Œì íŠ¸ì˜ ë¶€ëª¨ê°€ ì—†ìœ¼ë©´ ê·¸ëƒ¥ ë¦¬í„´í•´ì•¼í•¨

			tObj = CheckIfExist targetObj.parent		-- íƒ€ê²Ÿ ì˜¤ë¸Œì íŠ¸ ë¶€ëª¨ì˜ Ref Bone ì„ ì°¾ëŠ”ë‹¤.
			if tObj != undefined do (newObj.parent = tObj)		-- íƒ€ê²Ÿ ë¶€ëª¨ì˜ Ref Boneì„ ì°¾ì•˜ìœ¼ë©´ ìƒˆë¡œ ìƒê¸´ Ref Boneì˜ ë¶€ëª¨ë¡œ ë§í¬
		)
		
		-- obj ì˜ ëª¨ë“  ìì‹ë“¤ì„ ë°°ì—´ë¡œ ë¦¬í„´. ë°°ì—´ ìˆœì„œëŠ” ê³„ì¸µêµ¬ì¡° ìˆœì„œëŒ€ë¡œ
		function GetAllChildren obj = (
			if ( obj == undefined ) do return undefined
			
			local tAllChildren = #()
			if ( obj.children.count != 0 ) do (
				for o in obj.children do (
					append tAllChildren o
					if ( o.children.count != 0 ) do (
						tAllChildren = tAllChildren +  (GetAllChildren o)		-- recursive
					)
				)
			)
			
			return tAllChildren
		)
		
		-- ì„ íƒì´ ë°”ì´íŒ¨ë“œë¼ë©´, ì†Œì†ëœ ëª¨ë“  ë°”ì´íŒ¨ë“œë¥¼ ë¦¬í„´.
		function GetAllBiped obj = (
			if ( obj == undefined ) do return undefined
			if ( classof obj.baseobject != Biped_Object ) do return undefined
			
			local bipedRoot = obj.controller.rootNode
			
			local allChildren = GetAllChildren bipedRoot
			
			local allBiped = #()
			for o in allChildren do
			(
				if classof o.baseobject == Biped_Object do
				(
					append allBiped o
				)
			)
			return allBiped
		)

----------------------------------------------------------------------------------------------------------------

		fn fnInitDotTabs =
		(
			lblTips.font       = dotnetobject "System.Drawing.Font" "Roboto" 8
			lblTips.TextAlign  = (dotnetclass "system.drawing.contentalignment").MiddleCenter
			lblTips.BackColor  = (dotColor.FromArgb	255 250 250)
			lblTips.ForeColor  = (dotColor.FromArgb 60 0 160)
			tabBoxMan.sizeMode  = (dotnetclass "System.Windows.Forms.TabSizeMode").Fixed
			tabBoxMan.itemSize  = dotNetObject "System.Drawing.Size" 60 30
			tabBoxMan.dock      = tabBoxMan.dock.Fill
			tabBoxMan.Drawmode  = tabBoxMan.Drawmode.OwnerDrawFixed
	
			for aTab = 1 to arrAllToolsTab.count do
			(
				tabBoxMan.TabPages.add arrAllToolsTab[aTab][1]
			)
		)

		fn fnChangeToolsVisble id =
		(
			for i = 1 to arrAllToolsTab.count do 
			(
				if i == id then
				(
					for j in arrAllToolsTab[i][2] do j.visible = true
				)
				else (for j in arrAllToolsTab[i][2] do j.visible = false)
			)
			LastSubRollout = id
		)

		fn fnCreateBoxBody =
		(
			LayerManager.newLayerFromName "001_Ref_Boxes"
			tmpLayer = LayerManager.getLayerFromName "001_Ref_Boxes"

			for i in selection do with undo off 
			(
				c = snapshot i
				c.transform = matrix3 1
				bb = c.max - c.min
				b = box width:bb[1] length:bb[2] height:bb[3]
				CenterPivot b
				delete c
				b.transform = i.transform
				b.pos = i.center
			-- 	b.pivot = i.pivot
				b.name = i.name + "_BsBoxManBox"
				lc = Link_Constraint()
				b.transform.controller = lc
				lc.key_mode = 0
				lc.addTarget i 0
				-- freeze b
				b.showFrozenInGray = off

				tmplayer.addnode b
			)
		)

		fn fnCapitalizeFirstLetter str = 
		(
			if str != "" and str != undefined then
			(
				local firstLetter = substring str 1 1
				local remainingLetters = substring str 2 (str.count - 1)
				return (toupper firstLetter) + remainingLetters
			)
		)

		fn fnRefreshUnite =
		(
			strMertic = (fnCapitalizeFirstLetter (units.metricType as string))
			strSysType = (fnCapitalizeFirstLetter (units.systemType as string))
			idMetric = (finditem rolBsBoxMan.ddlMetric.items strMertic)
			idSysType = (finditem rolBsBoxMan.ddlSystemUnit.items strSysType)
			if idMetric != undefined and idSysType != undefined then
			(
				rolBsBoxMan.ddlMetric.Selection = idMetric
				rolBsBoxMan.ddlSystemUnit.Selection = idSysType
			)
		)

		on tabBoxMan Selected itm do
		(
			arrID = (itm.TabPageIndex + 1)
			if LastSubRollout != arrID do --é¿å…ç‚¹å‡»é‡å¤
			(
				fnChangeToolsVisble arrID
			) 
		)
		
		on rolBsBoxMan open do
		(
			fnInitDotTabs()
			fnChangeToolsVisble (tabBoxMan.SelectedIndex + 1)
			fnRefreshUnite()
			fnRefreshSliderValue()

			callbacks.addScript #filePostOpen "rolBsBoxMan.fnRefreshUnite ()" id:#BsBoxManCallbackOpen
			callbacks.addScript #systemPostNew "rolBsBoxMan.fnRefreshUnite ()" id:#BsBoxManCallbackNew
			callbacks.addScript #systemPostReset "rolBsBoxMan.fnRefreshUnite ()" id:#BsBoxManCallbackReset
			callbacks.addScript #selectionSetChanged "rolBsBoxMan.fnRefreshUnite ()" id:#BsBoxManSelChange
		)

		on btnClose pressed do 
		(
			try (destroydialog rolBsBoxMan) catch ()

			callbacks.removeScripts id:#BsBoxManCallbackOpen
			callbacks.removeScripts id:#BsBoxManCallbackNew
			callbacks.removeScripts id:#BsBoxManCallbackReset
			callbacks.removeScripts id:#BsBoxManSelChange
		)

		on rolBsBoxMan mbuttondown pos do 
		(
			try (destroydialog rolBsBoxMan) catch ()
		)
		
		on rolBsBoxMan lbuttondown posMou do
		(
			setSysCur #move
			offsetBsBoxMan = posMou
			dragStateBsBoxMan = on
		)
		
		on rolBsBoxMan lbuttonup posMou do
		(
			dragStateBsBoxMan = off
		)
		
		on rolBsBoxMan mouseMove pos do
		(
			if dragStateBsBoxMan == on then
			(
				SetDialogPos rolBsBoxMan (mouse.screenpos - offsetBsBoxMan)
			)
		)

		on chkCreateBodyBox changed state do
		(
			chkCreateBodyBox.state = true
			fnCreateBoxBody()
		)

		on btnClearBox pressed do 
		(
			try(delete $'*_BsBoxManBox';messagebox "æ¸…é™¤BsBoxæˆåŠŸ!                      " title:"BsBoxMan")
			catch(messagebox "é”™è¯¯ï¼šæ¸…é™¤BsBoxå¤±è´¥!                      " title:"BsBoxMan")
		)

		on ddlMetric selected id do
		(
			units.displayType= #Metric
			units.metricType= (ddlMetric.items[id] as name)
		)

		on ddlSystemUnit selected id do
		(
			units.SystemType= (ddlSystemUnit.items[id] as name)
		)

		on btnGetUnit pressed do 
		(
			fnRefreshUnite()
		)

		on chkProcess changed state do (
			chkProcess.state = true
			if selection.count == 0 do return ()		-- ì„ íƒëœ ì˜¤ë¸Œì íŠ¸ê°€ ì—†ìœ¼ë©´ ê·¸ëƒ¥ ë¦¬í„´
			
			undo on (
				
				LayerManager.newLayerFromName "001_Ref_Bones"   -- ë ˆì´ì–´ ìƒì„±.
				tmpLayer = LayerManager.getLayerFromName "001_Ref_Bones"  --ìƒì„±í•œ ë ˆì´ì–´ ê°€ì ¸ì˜¤ê¸°.
						-- Undo ê°€ ê°€ëŠ¥í•˜ë„ë¡
				local tTarget = #()		-- ë¶€ëª¨ ë§í¬ ì‘ì—…ì„ ìœ„í•´ì„œëŠ” ëª¨ë“  Ref Bone ì´ ìƒê²¨ë‚œ ë’¤ì— for ë¬¸ì„ í•œ ë²ˆ ë” ëŒë ¤ì•¼í•˜ë¯€ë¡œ ì‘ì—… ëŒ€ìƒì„ ê¸°ì–µì‹œí‚¤ëŠ” ë°°ì—´ì´ í•„ìš”í•¨
				local tNew = #()			-- ë¶€ëª¨ ë§í¬ ì‘ì—…ì„ ìœ„í•´ì„œëŠ” ëª¨ë“  Ref Bone ì´ ìƒê²¨ë‚œ ë’¤ì— for ë¬¸ì„ í•œ ë²ˆ ë” ëŒë ¤ì•¼í•˜ë¯€ë¡œ ì‘ì—… ëŒ€ìƒì„ ê¸°ì–µì‹œí‚¤ëŠ” ë°°ì—´ì´ í•„ìš”í•¨
				for o in selection do (
					checkStr = CheckWorkingBone o
					case of (
						(checkStr == "CREATE"):(
							-- ë°”ì´íŒ¨ë“œë¥¼ ì„ íƒí•œ ì±„ Processë¥¼ ëˆŒë €ìŒ. Refë³¸ì„ ìƒì„±í•˜ê±°ë‚˜ ê¸°ì¡´ Refë³¸ì„ ìˆ˜ì •í•¨.
							local tReply = #yes		-- ìœ ì €ì˜ ëŒ€ë‹µì„ ê¸°ì–µí•˜ëŠ” ë¡œì»¬ ë³€ìˆ˜
							local foundRefBone = CheckIfExist o
							
							local tBone
							if foundRefBone != undefined then (
								tBone = foundRefBone
							)
							else (
								tBone = CreateBone o.transform.pos (FindLookAtPos o) o.dir ((GetWidthX o) * (uiSpnBoneWidth.value * 0.01))
							)
							
							SetRefBoneAttr o tBone		-- RefBone ì†ì„± ì§€ì •
							SetRefController o tBone		-- ì»¨íŠ¸ë¡¤ëŸ¬ ë³€ê²½
							if ((classof o.baseobject) != Point AND (classof o.baseobject) != Dummy) do
							(
								o.boxMode = on
							)
							append tTarget o
							append tNew tBone
						)
						(checkStr == "MODIFY"):(
							-- Refë³¸ì„ ì„ íƒí•œ ì±„ Processë¥¼ ëˆŒë €ìŒ. Refë³¸ì„ ìˆ˜ì •í•˜ê¸°ë§Œ í•¨'
							-- ì´ ê²½ìš° oê°€ Refë³¸
							local tOrigin = GetRefOrigin o
	
							SetRefBoneAttr tOrigin o		-- RefBone ì†ì„± ì§€ì •
							SetRefController tOrigin o		-- ì»¨íŠ¸ë¡¤ëŸ¬ ë³€ê²½
	
							if ((classof tOrigin.baseobject) != Point AND (classof tOrigin.baseobject) != Dummy) do (
								tOrigin.boxMode = on
							)
							append tTarget tOrigin
							append tNew o
						)
						default:()
					)
				) -- for end
				if tTarget.count != 0 do (		-- CheckWorkingBone ì²´í¬ì— ì˜í•´ tTarget ê°œìˆ˜ê°€ 0ì¼ ê°€ëŠ¥ì„±ì´ ìˆìŒ.
					--for o = 1 to tTarget.count do (AutoParent tTarget[o] tNew[o])
				)
				
				select tNew

				for i in selection do tmplayer.addnode i
			)
		)
		
		on uiBtnSelect pressed do (
			tSel = #()
			for o in objects do (
				if ((getUserProp o IDKEYWORD) == true) do (append tSel o)
			)
			select tSel
		)
		
		on uiSpnBoneWidth changed var do (
			if selection.count == 0 do return ()
			tObj = #()
			for o in selection do (
				if (getUserProp o IDKEYWORD) == true do (append tObj o)		-- ì„ íƒ ì˜¤ë¸Œì íŠ¸ë“¤ ì•ˆì—ì„œ Ref Bone ë§Œ ê³¨ë¼ë‚¸ë‹¤
			)
			if tObj.count == 0 do return ()
			
			for o in tObj do (
				tNode = maxOps.getNodeByHandle (getUserProp o IDKEYWORDHANDLE)		-- í•¸ë“¤ ë²ˆí˜¸ë¡œ íƒ€ê²Ÿ ë³¸ì„ ì—­ìœ¼ë¡œ ì•Œì•„ë‚¸ë‹¤. ëª»ì°¾ìœ¼ë©´ tNode ëŠ” undefined
				tScale = 1.0 / ((tNode.transform.scale.y + tNode.transform.scale.z) * 0.5)
				if tNode != undefined do (
					tValue = (GetWidthX tNode) * (uiSpnBoneWidth.value * 0.01 * tScale)
					o.width = tValue
					o.height = tValue
				)
			)			
		)
		
		on uiColorWireColor changed var do (
			for o in selection do (
				if ((getUserProp o IDKEYWORD) == true) do (o.wirecolor = var)
			)
		)
		
		-- ìŠ¤ì¼€ì¼ì‹œ 1ë²ˆ ì²™ì¶”ì™€ ì‡„ê³¨ì˜ ìœ„ì¹˜ê°€ ì—‰ëš±í•œ ê³³ì— ìˆê²Œëœë‹¤. ì´ê²ƒì„ ë³´ì •í•´ì£¼ëŠ” Position ì„œë¸Œì• ë‹˜ ì»¨íŠ¸ë¡¤ëŸ¬ë¥¼ ì¶”ê°€í•´ì•¼í•¨.
		-- on uiBtnSetScalableBiped pressed do
		-- (
		-- 	if selection[1] == undefined do return()
		-- 	if (classof selection[1].baseobject != Biped_Object) do return()
			
		
		-- 	local allBiped = GetAllBiped selection[1]
		-- 	local rootBiped = selection[1].controller.rootNode
		-- 	local pelvis = biped.getNode selection[1] #Pelvis link:1
			
		-- 	local newPoint = Point isSelected:off Box:on Centermarker:off axistripod:off cross:off size:((GetWidthX pelvis) * 2.2) wirecolor:green
		-- 	newPoint.transform = rootBiped.transform
		-- 	newPoint.parent = rootBiped
			
		-- 	-- í˜¹ì‹œ ëª¨ë¥¼ ì˜¤ë¥˜ë¥¼ ë°©ì§€í•˜ê¸° ìœ„í•´ figureëª¨ë“œëŠ” êº¼ì¤€ë‹¤.
		-- 	rootBiped.controller.figureMode = false
			
		-- 	--paramWire.connect $.transform.controller[#Scale] $'Bip001 L Forearm'.transform.controller.Biped_SubAnim.controller.BipScaleList.controller[#Bezier_Scale] "Scale"
	
		-- 	for o in allBiped do
		-- 	(
		-- 		if ( classof o.controller == BipSlave_Control) do
		-- 		(
		-- 			o.controller.Biped_SubAnim.controller.BipScaleList.Available.controller = bezier_scale()
		-- 			o.controller.Biped_SubAnim.controller.BipScaleList.active = o.controller.Biped_SubAnim.controller.BipScaleList.count
					
		-- 			paramWire.connect newPoint.transform.controller[#Scale] o.transform.controller.Biped_SubAnim.controller.BipScaleList.controller[o.controller.Biped_SubAnim.controller.BipScaleList.count] "Scale"
		-- 		)
		-- 	)
		-- 	select newPoint
		-- )
	
		--ì°Œë…¸ë‹˜ ì œì‘--
		on uiBtnDummy changed state do ( 
			uiBtnDummy.state = true
			LayerManager.newLayerFromName "001_Ref_Dummy"   -- ë ˆì´ì–´ ìƒì„±.
			tmpLayer = LayerManager.getLayerFromName "001_Ref_Dummy"  --ìƒì„±í•œ ë ˆì´ì–´ ê°€ì ¸ì˜¤ê¸°.
			sel = selection as array
	
			for s in sel do
			(
				--create point
				--tmpDummySize = uiDummySize.value * 1  -- 
				pt = point centerMaker:False axisTripod:False cross:True box:True size:uiDummySize.value
				pt.wirecolor = color 250 160 0
				--rename point
				pt.name = "RefPoint_" + s.name
				--match transform to selected object
				pt.transform = s.transform
				--assign rotation controller
				pt.rotation.controller = orientation_constraint()
				pt.position.controller = position_constraint()
				--append target
				pt.rotation.controller.appendTarget s 100
				pt.position.controller.appendTarget s 100
				
				tmplayer.addnode pt --ìƒˆì„±í•œ í¬ì¸íŠ¸ ë ˆì´ì–´ì— ë„£ê¸°.
				
			)
			
		)
		
		on uiBtnHelp pressed do (
			shellLaunch "http://cafe.naver.com/pinksox/5035" ""
		)
		
		on uiDummySize changed val do
		(
			for b in selection where classof b == BoneGeometry or classof b == dummy or classof b == point do
			(
				case classof b of
				(
					(BoneGeometry):(b.width = val;b.height = val)
					(Dummy):(b.boxsize = [val,val,val])
					(point):(b.size = val)
				)
			)
		)

		on uiBtnClearAll pressed do
		(
			delLayerA    = LayerManager.getLayerFromName "001_Ref_Bones"
			delLayerB	 = LayerManager.getLayerFromName "001_Ref_Dummy"
			
			if delLayerA != undefined then with undo on
			(
				delLayerObj = #()
				layerRTA = delLayerA.nodes &delLayerObj
				delLayerA.lock = off
				tDel = #()
				for o in delLayerObj do (
					if ((getUserProp o IDKEYWORD) == true) do (delete o)
				)
				LayerManager.deleteLayerByName "001_Ref_Bones"
				delLayerObj = #()

				messageBox "æ¸…ç† RefBones æˆåŠŸï¼				" title:"BsBoxMan"
			)

			if delLayerB != undefined then with undo on
			(
				delLayerObj = #()
				layerRTB = delLayerB.nodes &delLayerObj
				delLayerB.lock = off
				for j in delLayerObj where (delLayerObj.count != 0) do 
				(
					if ((matchpattern j.name pattern:"RefPoint_*") == true) do 
					delete j
				)
				LayerManager.deleteLayerByName "001_Ref_Dummy"
				delLayerObj = #()

				messageBox "æ¸…ç† RefPoints æˆåŠŸï¼				" title:"BsBoxMan"
			)
		)

		fn getRandomColor =  
		(  
			local randomColor = blue  
			if radColor.state == 1 then  
			(  
				randomColor.hue = random 0 255  
				randomColor.saturation = 120  
				randomColor.value = random 150 240  
			)  
			else if radColor.state == 2 then       
			(  
				randomColor = white * random 0.2 0.8  
			)  
			else if radColor.state == 3 then  
			(  
				randomColor.hue = random 0 255  
				randomColor.saturation = 255  
			)
			else if radColor.state == 4 then  
			(  	
				local r = random 5 30	
				randomColor = color (r*6) (r*2) r
			)
			else if radColor.state == 5 then       
			(  
				randomColor.hue = random 0 255  
				randomColor.saturation = 30  
				-- randomColor.value = 180
			)
			else if radColor.state == 6 then  
			(  
				randomColor.hue = random 0 255 
				randomColor.saturation = 90  
				randomColor.value = 90 
			)  
			else if radColor.state == 7 then  
			(  	
				randomColor.hue = random 40 140  
				randomColor.saturation = 255  
				randomColor.value = 255
			)
			else if radColor.state == 8 then  
			(  
				randomColor = random black white  
			)
			return randomColor  
		)  

		on btnRandom changed state do  
		(  
			btnRandom.state = true
			with undo on
			(	
				if radObject.state == 1 then  
				(  
					for i in geometry do  
					(  
						InstanceMgr.GetInstances i &instArray  
						if chkIsInstance.checked then  
						(  
							instArray.wirecolor = getRandomColor()   
						)  
						else  
						(  
							i.wirecolor = getRandomColor()   
						)  
					)  
				) 
				else if radObject.state == 2 then  
				(  
					for i in shapes do  
					(  
						InstanceMgr.GetInstances i &instArray  
						if chkIsInstance.checked then  
						(  
							instArray.wirecolor = getRandomColor()   
						)  
						else  
						(  
							i.wirecolor = getRandomColor()   
						)  
					)  
				)
				else if radObject.state == 3 then  
				(  
					allGroups = for obj in objects where isGroupHead obj collect obj  
					if allGroups.count == 0 then  
					(  
						messageBox "There are no groups in the scene."  
					)  
					else  
					(  
						fn changeInstancesRandom group =  
						(  
							local randomColor = getRandomColor()   
							for i in group do  
							(  
								InstanceMgr.GetInstances i &instArray  
								instArray.wirecolor = randomColor	
							)
						) 
						fn changeWireColorRandom group =  
						(  
							local randomColor = getRandomColor()   
							for i in group do  
							(
								i.wirecolor = randomColor  
							)
						)
						
						if chkIsInstance.checked then
						(
							for group in allGroups do  
							(  
								changeInstancesRandom group  
							)
						)
						for group in allGroups do  
						(  
							changeWireColorRandom group  
						)						
					)  
				) 
				else if radObject.state == 4 then  
				(  
					for i in objects do  
					(  
						InstanceMgr.GetInstances i &instArray  
						if chkIsInstance.checked then  
						(  
							instArray.wirecolor = getRandomColor()   
						)  
						else  
						(  
							i.wirecolor = getRandomColor()   
						)  
					)  
				)  				
				else if radObject.state == 5 then  
				(  
					if selection.count < 1 then messagebox "Please select at least one object"  
					else  
					(  
						for i in selection do  
						(  
							InstanceMgr.GetInstances i &instArray  
							if chkIsInstance.checked then  
							(  
								instArray.wirecolor = getRandomColor()   
							)  
							else  
							(  
								i.wirecolor = getRandomColor()   
							)  
						)  
					)  
				) 
			)
			redrawviews()   
		)  
		fn FnGradient =  
		(  
			local domain = if selection.count == 0 then objects else selection  
			local cnt = domain.count  
			if cnt >= 2 then  
			(  
				local col1 = cpGradient1.color  
				local col2 = cpGradient2.color  
				local step = ( col2 - col1 ) / ( cnt - 1 )  
				for i = 1 to cnt do domain[i].wireColor = col1 + ( i - 1 ) * step  
			)  
			redrawviews()   
		)  

		on cpGradient1 changed val do FnGradient()   
		on cpGradient2 changed val do FnGradient()   

		on cp changed newColor do  
		(  
			if selection.count > 0 then  
			(  
				for obj in selection do  
				(  
					selection.wirecolor = newColor  
				)  
			)  
			else  
			(  
				for obj in geometry do  
				(  
					geometry.wirecolor = newColor  
				)  
			)  
		)  
		
		fn FnMatS objs =  
		(  
			for i in objs do  
			(  
				if superClassOf i == geometryClass then  
				(  
					newColor = i.wireColor  
					i.material = standardMaterial showInViewport:true name:  ( "WireColor Pro - " + i.name )  
					i.material.diffuse = newColor  
				)  
			)  
			redrawviews()   
		)  

		fn FnMatP objs =  
		(  
			for i in objs do  
			(  
				if superClassOf i == geometryClass then  
				(  
					newColor = i.wireColor  
					i.material = physicalMaterial showInViewport:true name:  ( "WireColor Pro - " + i.name )  
					i.material.Base_Color = newColor  
				)  
			)  
			redrawviews()   
		)  

		on btnConverter pressed do  
		(  
			if selection.count < 1 then  
			(  
				messagebox "Please select at least one object"  
			)  
			else  
			(  
				if radMaterial.state == 1 then  
				(  
					FnMatS selection  
				)  
				else if radMaterial.state == 2 then  
				(  
					FnMatP selection  
				)  
			)  
		)  
		on btnErase pressed do  
		(  
			if radErase.state == 1 then  
			(  
				if selection.count < 1 then  
				(  
					messagebox "Please select at least one object"  
				)  
				else  
				(  
					$.material = undefined  
				)  
			)  
			else  
			(  
				if geometry.count < 1 then  
				(  
					messagebox "You have no geometry objects"  
				)  
				else  
				(  
					geometry.material = undefined  
				)  
			)  
		)  

		-- End  
		
		local selfValue = 100
		local arrSelObj = #()
	
	
		fn jugdeSel =
		(
			if (selection.count == 0) then
			(
				arrSelObj = #()
				for i in objects do (append arrSelObj i)
			)
			else
			(
				arrSelObj = #()
				for i in selection do (append arrSelObj i)
			)
		)
	
		fn fnSetSelfIllumAmount arrSelObj val = 
		(
			for i in arrSelObj do
			(
				if (classof i.mat == Multimaterial) do  
				(
					for s = 1 to i.mat.materialList.count do  i.mat.materialList[s].selfIllumAmount = val
				)
				if (classof i.mat == Standardmaterial) do 
				(
					i.mat.selfIllumAmount = val
				)
			)
		)
	
		on edtValue entered val do
		(
			jugdeSel ()
			if ((val != ".") and (val as integer != undefined) and (val != "") and (val as integer <= 100) and (val as integer >= 0)) then
			(
				sldValue.value = (val as integer)
			)
		)
	
		on sldValue changed val do (jugdeSel ();fnSetSelfIllumAmount arrSelObj val;edtValue.text = val as string)
	
		on btnSet pressed do 
		(
			jugdeSel ()
			if ((edtValue.text != ".") and (edtValue.text as integer != undefined) and (edtValue.text != "") and (edtValue.text as integer <= 100) and (edtValue.text as integer >= 0)) then
			(
				fnSetSelfIllumAmount arrSelObj (edtValue.text as integer)
				sldValue.value = (edtValue.text as integer)
			)
			else messagebox "-------------------------\r\nè¯·è¾“å…¥0-100çš„æ•´æ•°æ•°å€¼\r\n"
		)
		on btnSet0 pressed do (jugdeSel ();fnSetSelfIllumAmount arrSelObj 0;sldValue.value = 0;edtValue.text = "0")
	
		on btnSet100 pressed do (jugdeSel ();fnSetSelfIllumAmount arrSelObj 100;sldValue.value = 100;edtValue.text = "100")

		on rdoReduce changed state do
		(
			try(
				if rdoReduce.state == 2 then 
				(
					$.scale.x = (1.0/sldLenthValue.value)
					$.scale.y = (1.0/sldWidthValue.value)
					$.scale.z = (1.0/sldHeightValue.value)
				)
				else
				(
					$.scale.x = sldLenthValue.value
					$.scale.y = sldWidthValue.value
					$.scale.z = sldHeightValue.value
				)
			)catch()
		)

		on sldLenthValue changed val do 
		(
			fnRefreshSliderValue()
			try(if rdoReduce.state == 2 then $.scale.x = (1.0/val) else $.scale.x = val)catch()
		)

		on sldWidthValue changed val do 
		(
			fnRefreshSliderValue()
			try(if rdoReduce.state == 2 then $.scale.y = (1.0/val) else $.scale.y = val)catch()
		)

		on sldHeightValue changed val do 
		(
			fnRefreshSliderValue()
			try(if rdoReduce.state == 2 then $.scale.z = (1.0/val) else $.scale.z = val)catch()
		)

		on ckbReset changed state do 
		(
			--å¯ä»¥è®°å½•ä¹‹å‰é•¿å®½é«˜åœ¨è‡ªå®šä¹‰å±æ€§é‡Œé¢
			ckbReset.state = true
			try 
			(
				$.scale = [1,1,1]
				sldLenthValue.value = 1
				sldWidthValue.value = 1
				sldHeightValue.value = 1
			)
			catch(messagebox "ä»…æ”¯æŒåˆ›å»ºçš„ Boxesï¼è¯·å‹¿é€‰æ‹©å…¶ä»–ç±»å‹ï¼                 " title:"BsBoxMan")
			fnRefreshSliderValue()
		)

		on lblTips mouseup arg do
		(
			shellLaunch "https://space.bilibili.com/2031113/channel/collectiondetail?sid=560782" ""
		)
	)
	createDialog rolBsBoxMan style:#()
)