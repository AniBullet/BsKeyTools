/*
* @Description: å¿«é€Ÿæ‰“å¼€maxæ–‡ä»¶ï¼Œå¹¶ä¿å­˜å¸¸ç”¨ç›®å½•
* @Author: Bullet.S
* @Date: 2019-11-23 01:41:13
 * @LastEditors: Bullet.S
 * @LastEditTime: 2025-08-17 04:44:37
* @Email: animator.bullet@foxmail.com
*/


try(destroydialog rolBsOpenTools)catch()
try(destroydialog rolAddItem)catch()

struct itemsFolder (name,dir)
Global iniPosOpenTools
Global iniWidthOpenTools = 340
Global iniDesktop        = (itemsFolder name:"Desktop" dir:"")
Global iniSelectedID     = 1
Global iniLikedFolder    = #()
Global iniFilterPrefix   = #()

try(FileIn ((getDir #scripts) + "\\BulletScripts\\fnSaveLoadConfig.ms"))
catch(messagebox "è„šæœ¬å®‰è£…å¯èƒ½ä¸å®Œå…¨,å»ºè®®é‡æ–°å®‰è£…...        " beep:false)
try(FileIn ((getDir #scripts) + "\\BulletScripts\\fnGetColorTheme.ms"))
catch(messagebox "è„šæœ¬å®‰è£…å¯èƒ½ä¸å®Œå…¨,å»ºè®®é‡æ–°å®‰è£…...        " beep:false)
stLoadConfigAll.fnLoadConfigBsOpenTools ()

struct itemsFolder (name,dir)
global dateTime = (dotNetClass "System.DateTime").Now
global arrDayWeek = #("æ—¥", "ä¸€", "äºŒ", "ä¸‰", "å››", "äº”", "å…­")
Global BulletConfig = execute ("@\"" + (getDir #maxData) + "\\BulletConfig.ini\"")  --é…ç½®æ–‡ä»¶è·¯å¾„

-- Global iniRefPicDir			= (itemsFolder name:"RefPicDir" dir:"")
Global posMouMove         = [0,0]
Global switchMouState     = false

Global arrFolders         = #()
Global arrFoldersName     = #()
Global arrFiles           = #()
Global arrFilesName       = #()
Global dirLiked           = ""
Global dirOpened          = ""
Global arrDirItems        = #()
Global arrPreItems        = #()
Global arrRecentFiles     = #()
Global arrRecentFilesName = #()
Global arrFileType        = #(".max",".fbx",".bip",".ms*", ".mzp")
Global rolBsOpenTools
Global fnRefreshList
Global tempLastFolder

rollout rolAddItem "æ·»åŠ å¸¸ç”¨è·¯å¾„" width:340 height:80
(
	groupbox gbxAddDir "æ·»åŠ å¸¸ç”¨è·¯å¾„  ---------- Tipsï¼šå³å‡»æ¡†å†…æ¡ç›®åˆ é™¤ " width:330 height:70 pos:[5,5]
	
	edittext edtAddDir "æ”¶è—å‘½å" pos:[10,50] fieldWidth:210 height:20 labelOnTop:false text:""
	button btnOpenDir "é€‰æ‹©ç›®å½•"  pos:[275,25] width:55 height:20 tooltip:"é€‰æ‹©æ·»åŠ ç›®å½•ï¼Œé»˜è®¤æ‰“å¼€å½“å‰æ–‡ä»¶ç›®å½•"
	edittext edtDirStr "" pos:[5,25] fieldWidth:262 height:20 readOnly:true
	
	-- groupbox gbxAddPre "æ·»åŠ è¿‡æ»¤å‰ç¼€  ---------- Tipsï¼šå³å‡»æ¡†å†…æ¡ç›®åˆ é™¤ "  width:330 height:70 pos:[5,5]
	-- edittext edtPrefix ""  text:"" fieldWidth:262 height:35 pos:[5,35]

	button btnAddItems "æ·»åŠ " width:55 height:25 pos:[275,45]

	fn fnAddToFavorite =
	(
		if ((edtDirStr.text != "") and (edtAddDir.text != "") and (edtDirStr.visible == true)) then
		(
			local fnExist = 0
			for i = 1 to iniLikedFolder.count do 
			(
				if matchPattern iniLikedFolder[i].dir pattern:edtDirStr.text then 
				(
					fnExist = 1
				)
			)
			if fnExist == 0 then
			(
				append iniLikedFolder (itemsFolder edtAddDir.text edtDirStr.text)
				arrDirItems = #()
				for i = iniLikedFolder.count to 1 by -1 do
				(
					append arrDirItems iniLikedFolder[i].name
				)
				rolBsOpenTools.ltbLikedFolder.items = arrDirItems
				rolBsOpenTools.ltbLikedFolder.selection = 1
				-- fnRefreshList rolBsOpenTools.edtBrowseFolder.text type:arrFileType[rolBsOpenTools.rdoFileType.state]
			)
			else (messagebox "----------------------------------------\r\nå¯èƒ½å¸¸ç”¨ç›®å½•ä¸­å·²å­˜åœ¨æ­¤ç›®å½•~")
		)
	)

	on btnOpenDir pressed do 
	(
		local dir = getSavePath caption:"è¯·é€‰æ‹©ç›®å½•:" initialDir:(maxFilePath)
		if (dir != undefined) then
		(
			edtDirStr.text = dir
		)
	)

	on edtAddDir changed txt do 
	(
		tempText = substituteString txt "\n" ""
		edtAddDir.text = tempText
		-- print edtAddDir.text
	)

	on edtPrefix changed txt do 
	(
		tempText = substituteString txt "\n" ""
		edtPrefix.text = tempText
		-- print edtPrefix.text
	)

	on btnAddItems pressed do 
	(
		fnAddToFavorite()
		try(destroydialog rolAddItem)catch()
		rolBsOpenTools.fnRefreshList rolBsOpenTools.edtBrowseFolder.text type:arrFileType[rolBsOpenTools.rdoFileType.state]
		iniSelectedID = rolBsOpenTools.ltbLikedFolder.selection
		stSetConfigAll.fnSetConfigBsOpenTools ()
	)
)

rollout rolBsOpenTools "" width:580 height:400
(
	groupbox grpMain "æ—¶å…‰æœº_v1.2" \
	width:570 height:395 pos:[5,0]
	button btnClose "X" pos:[rolBsOpenTools.width - 15,0] height:15 width:15 border:false
	editText edtBrowseFolder "" text:"" labelOnTop:false align:#right \
	height:20 fieldWidth:425 pos:[70,22] readOnly:true 
	button btnOpenAddress "..." align:#right tooltip:"é€‰æ‹©æ‰“å¼€ç›®å½•" \
	height:22 width:30 pos:[37,20] border:false
	button btnAddPathToVavorite "âœš" align:#left border:false \
	height:22 width:30 pos:[505,20] tooltip:"æ·»åŠ å½“å‰è·¯å¾„è‡³å¸¸ç”¨ç›®å½•"
	button btnOpenCurrentDir "O" tooltip:"æ‰“å¼€å½“å‰æ–‡ä»¶å¤¹" \
	height:22 width:30 pos:[535,20] border:false
	-- button btnRemoveFolder "â”" align:#left \
	-- height:20 width:25 pos:[10,20] tooltip:"åˆ é™¤é€‰ä¸­çš„æ”¶è—ç›®å½•"
	-- button btnAddFolder "âœš" align:#left border:false \
	-- height:20 width:20 pos:[35,20] tooltip:"æ–°å¢å¸¸ç”¨ç›®å½•"
	button btnRefreshFolder "â†»" align:#left border:false \
	height:22 width:25 pos:[10,20] tooltip:"å·¦ï¼šåˆ·æ–°å½“å‰æ–‡ä»¶ç›®å½•\r\n\r\nå³ï¼šé‡ç½®åœºæ™¯"
	label lblLikedFolderTitle "å¸¸ç”¨ç›®å½•" align:#left \
	height:18 width:60 pos:[15,50]
	button btnAddLikedFolder "ï¼‹" border:true \
	height:18 width:18 pos:[75,50] tooltip:"æ·»åŠ å¸¸ç”¨ç›®å½•"
	button btnDelLikedFolder "ï¼" border:true \
	height:18 width:18 pos:[95,50] tooltip:"åˆ é™¤é€‰ä¸­å¸¸ç”¨ç›®å½•"
	listbox ltbLikedFolder "" selection:0 \
	height:22 Width:105 pos:[10,75]
	editText edtInputFilter "" text:"" labelOnTop:false align:#left \
	height:17 fieldWidth:100 pos:[115,75]
	listBox ltbFilesList "" align:#right selection:0 \
	height:22 width:340 pos:[225,75]
	label lblFilterPrefixTitle "è¿‡æ»¤è¯ç¼€" align:#left \
	height:18 width:60 pos:[125,50]
	button btnAddFilterPrefix "ï¼‹" border:true \
	height:18 width:18 pos:[180,50] tooltip:"æ·»åŠ è¿‡æ»¤è¯ç¼€"
	button btnDelFilterPrefix "ï¼" border:true \
	height:18 width:18 pos:[200,50] tooltip:"åˆ é™¤é€‰ä¸­è¿‡æ»¤è¯ç¼€"
	listBox ltbFilterPrefix "" align:#left selection:0 \
	height:10 width:100 pos:[120,100]
	checkbutton ckbReverse "å€’åº" pos:[420,50] height:18 width:40 border:true
	button btnPrevFolder "ä¸Šå±‚" tooltip:"è¿”å›ä¸Šå±‚ç›®å½•ï¼Œä¹Ÿå¯å³å‡»é¢æ¿ç©ºç™½å¤„" \
	height:18 width:40 pos:[465,50] border:true
	checkbutton chePreview "é¢„è§ˆ >>" pos:[510,50] height:18 width:55 border:true tooltip:"å±•å¼€é¢„è§ˆçª—å£" checked:false
	
	-- é¢„è§ˆçª—å£ç›¸å…³UIå…ƒç´ 
	groupbox grpPreview "Maxæ–‡ä»¶é¢„è§ˆçª—å£ï¼ŒFBXä¼šå¼¹å‡ºé¢å¤–é¢„è§ˆçª—" width:360 height:200 pos:[585,5] visible:false
	dotNetControl dncMaxPreview "System.Windows.Forms.PictureBox" width:350 height:170 pos:[590,25]
	
	-- æ–‡ä»¶å±æ€§æ˜¾ç¤ºåŒºåŸŸ
	groupbox grpFileInfo "æ–‡ä»¶å±æ€§" width:360 height:155 pos:[585,210] visible:false
	label lblFileInfo "" width:340 height:120 pos:[590,230] align:#left
	
	-- æ–‡ä»¶æ“ä½œæŒ‰é’®
	button btnCopyPath "å¤åˆ¶è·¯å¾„" width:175 height:20 pos:[585,375] tooltip:"å¤åˆ¶æ–‡ä»¶å®Œæ•´è·¯å¾„åˆ°å‰ªè´´æ¿"
	button btnCopyFileName "å¤åˆ¶æ–‡ä»¶å" width:175 height:20 pos:[770,375] tooltip:"å¤åˆ¶æ–‡ä»¶ååˆ°å‰ªè´´æ¿"
	radioButtons rdoFileType "" columns:4 \
	pos:[230,50] labels:#(".max",".fbx",".bip",".ms") 

	checkbox ckbSilentOpen "é™é»˜æ‰“å¼€Max" pos:[125,240] width:90 height:15 checked:true
	button btnRecentFileDir "æœ€è¿‘æ‰“å¼€" border:true \
	height:25 width:100 pos:[120,260] tooltip:"æœ€è¿‘æ‰“å¼€æ–‡ä»¶"
	button btnDesktopDir "æ¡Œé¢è‡ªå®š" border:true \
	height:25 width:100 pos:[120,285] tooltip:"è‡ªå®šæ–‡ä»¶å¤¹ï¼Œé»˜è®¤æ¡Œé¢ï¼Œå³é”®å¯é‡è®¾"
	button btnScriptsDir "è„šæœ¬ç›®å½•" border:true \
	height:25 width:100 pos:[120,310] tooltip:"è„šæœ¬æ–‡ä»¶å¤¹\r\nå·¦ï¼šæ ¹ç›®å½•\r\nå³ï¼šè‡ªå¯ç›®å½•"
	button btnAutobackDir "è‡ªåŠ¨ä¿å­˜" border:true \
	height:25 width:100 pos:[120,335] tooltip:"è‡ªåŠ¨ä¿å­˜ç›®å½•"
	-- button btnPreviewDir "<è½®å›æ¢¦å¢ƒ>" border:false \
	-- height:25 width:70 pos:[10,310] tooltip:"å·¦ï¼šé¢„è§ˆè§†é¢‘é»˜è®¤æ–‡ä»¶å¤¹\r\nå³ï¼šå¿«é€Ÿæ‹å±æ¸²æŸ“é¢„è§ˆåŠ¨ç”»"
	groupBox grpDate "" width:570 height:30 pos:[5,365]
	label lblLikedFolder "" height:15 width:140 pos:[10,375]
	label lblCountTips "" height:15 width:80 pos:[470,375]
	label lblDateTime "" height:15 width:140 pos:[180,375] 
	HyperLink lnkLink "Bullet.S" color:myfgColor hovercolor:myClickColor visitedcolor:myClickColor \
	pos:[330,375] address:"https://space.bilibili.com/2031113"
	-- checkbutton ckbLockWidth "ğŸ”’" pos:[86,312] width:20 height:20 border:false \
	-- toolTip:"æ˜¯å¦è§£é”å®½åº¦è°ƒèŠ‚\r\nå³ï¼šé‡ç½®å®½åº¦" checked:false
	-- slider sldSetWidth "" pos:[113,312] range:[340,540,iniWidthOpenTools] ticks:0 \
	-- width:222 type:#integer toolTip:"è°ƒèŠ‚é¢æ¿å®½åº¦,ä»¥æ˜¾ç¤ºæ›´å¤šå­—" enabled:false
	-- label labLogo "-------------------------  TECH OTAKUS SAVE THE WORLD -" pos:[12,365]
	------------------------------------------------- â†‘ UI -------------------------------------
	
	-- global charClass = dotNetClass "System.Char" --dotnet charclass used for isDigit comparisment
	-- dotNetClass:System.Char

	fn getFilesequenceFile f &base &digits = 
	(
		f = getFilenameFile f
		base = trimRight f "0123456789"
		digits = subString f (base.count + 1) -1
	)

	fn fnPseudoNaturalSort a b =  --æ–‡ä»¶åæ’åºæ–°æ–¹æ³•--https://forums.cgsociety.org/t/sorting-filenames/1219205/4
	(
		a = a as string
		b = b as string
		getFilesequenceFile a &aBase &aDigits
		-- hackhackhack.  This pads a number with zeros to 6 digits without using a loop.
		-- things will fail if there's more digits.. 6 'seems' safe.
		aDigits = subString ((1000000 + (aDigits as integer)) as string) 2 -1
		getFilesequenceFile b &bBase &bDigits
		bDigits = subString ((1000000 + (bDigits as integer)) as string) 2 -1
		a = aBase + aDigits
		b = bBase + bDigits
	
		case of (
		(a == b): 0
		(a < b): -1
		(a > b): 1
		)
	)
-------------------------------------â†‘ æ–‡ä»¶æ’åºæ–¹æ³• --------------------------------------------------

	Fn fnLoadRecentFileList = -------è·å–æœ€è¿‘æ‰“å¼€æ–‡ä»¶åˆ—è¡¨
	(
		local recentfiles = (getdir #maxData) + "RecentDocuments.xml"
		if doesfileexist recentfiles then
		(
			arrRecentFiles     = #()
			arrRecentFilesName = #()
			xDoc = dotnetobject "system.xml.xmldocument"	
			xDoc.Load recentfiles
			Rootelement = xDoc.documentelement

			arrRecentFiles = for i = 0 to rootelement.childnodes.item[4].childnodes.itemof[0].childnodes.count-1 collect 
			(
				rootelement.childnodes.item[4].childnodes.itemof[0].childnodes.itemof[i].childnodes.itemof[3].innertext	
			)
			Return arrRecentFiles
			LRXML = Undefined
			XDoc = Undefined
			XDoc = nothing	
		)
		if arrRecentFiles[1] != undefined then
		(
			for c in arrRecentFiles do  --è·å–æ–‡ä»¶å¤¹åå­—,åé¢åˆ‡æ¢è„šæœ¬ç±»åˆ«å’Œåˆ—è¡¨ä¼šç”¨åˆ°
			(
				append arrRecentFilesName ("ğŸ“„ " + (getFilenameFile (substring c 1 (c.count-1))))
			)
		)
	)

	fn fnGetLastFolder strFolder =
	(
		if ((strFolder != "") and (doesDirectoryExist strFolder)) then
		(
			local arrFilterStr = filterstring strFolder @"\"
			local strLastFolder = ""
			for i = 1 to (arrFilterStr.count - 1) do 
			(
				strLastFolder = strLastFolder  + arrFilterStr[i] + @"\\"
			)
		)
		return strLastFolder
	)
	
	fn fnRefreshList strFilesDir filename:(rolBsOpenTools.ltbFilterPrefix.selected) type:".max" =
	(
		arrFolders = #()
		arrFiles = #()
		arrFoldersName = #()
		arrFilesName = #()
		arrAll = #()
		arrFinal = #()
		if strFilesDir != undefined then 
		(
			if ((strFilesDir != "") and (doesDirectoryExist strFilesDir)) then
			(
				local tempDir = GetDirectories (strFilesDir + "/*")
				for i in tempDir do 
				(
					append arrFolders (substring i 1 (i.count-1))
				)
				qsort arrFolders fnPseudoNaturalSort
				(arrFilesBeforeFilter = getFiles (strFilesDir + "\\*" + type))
				qsort arrFilesBeforeFilter fnPseudoNaturalSort
				if (filename != undefined) then
				(
					for i in arrFilesBeforeFilter do 
					(
						if (matchpattern (getfilenamefile i) \
						pattern:("*" + filename + "*")) then 
						(append arrFiles i)
					)
					rolBsOpenTools.edtInputFilter.text = filename
				)
				else arrFiles = arrFilesBeforeFilter
				
				if arrFolders[1] != undefined then
				(
					for c in arrFolders do  --è·å–æ–‡ä»¶å¤¹åå­—,åé¢åˆ‡æ¢è„šæœ¬ç±»åˆ«å’Œåˆ—è¡¨ä¼šç”¨åˆ°
					(
						append arrFoldersName ("ğŸ“‚ " + (getFilenameFile c))
					)
				)
				if arrFiles[1] != undefined then
				(
					for c in arrFiles do  --è·å–æ–‡ä»¶åå­—,åé¢åˆ‡æ¢è„šæœ¬ç±»åˆ«å’Œåˆ—è¡¨ä¼šç”¨åˆ°
					(
						(append arrFilesName ("ğŸ“„ " + (getFilenameFile c) + type))
					)
				)
				if rolBsOpenTools.ckbReverse.state == true then 
				(
					arrAll = (arrFoldersName + arrFilesName)
					tempArray = for i = arrAll.count to 1 by -1 collect arrAll[i]
					arrFinal = for i in tempArray collect i
					rolBsOpenTools.ltbFilesList.items = arrFinal
				)
				else
				(
					rolBsOpenTools.ltbFilesList.items = arrFoldersName + arrFilesName
				)
				rolBsOpenTools.edtBrowseFolder.text = strFilesDir
				for i = 1 to iniLikedFolder.count do 
				(
					if matchPattern iniLikedFolder[i].dir pattern:strFilesDir then 
					(
						rolBsOpenTools.ltbLikedFolder.selection = iniLikedFolder.count + 1 - i
					)
				)
			)
			else 
			(
				if ((iniSelectedID != 0) and (iniLikedFolder.count > 0)) then
				(
					rolBsOpenTools.ltbLikedFolder.selection = iniSelectedID
					fnRefreshList iniLikedFolder[iniLikedFolder.count + 1 - iniSelectedID].dir
				)
			)
		)
		else messagebox "-------------------------------------\r\næ–‡ä»¶å¤¹å¯èƒ½å·²ä¸å­˜åœ¨\r\nè¯·å°è¯•åˆ·æ–°\r\n"
		lblCountTips.text = "æ–‡ä»¶ï¼š" + arrFiles.count as string
		lblLikedFolder.text = if ltbLikedFolder.selected == undefined then "è¯·æ·»åŠ å¸¸ç”¨ç›®å½•" else ltbLikedFolder.selected
	)
	
	fn fnRefreshAddress =
	(
		if (maxFilePath != "") then 
		(
			local tempMaxFilePath = (substring maxFilePath 1 (maxFilePath.count-1))
			fnRefreshList tempMaxFilePath type:arrFileType[rdoFileType.state]
		)
		else 
		(
			if rolBsOpenTools.ltbLikedFolder.selection != 0 then 
			(
				local tempSelectionID = rolBsOpenTools.ltbLikedFolder.selection
				edtBrowseFolder.text = iniLikedFolder[iniLikedFolder.count + 1 - tempSelectionID].dir
			)
			else 
			(
				edtBrowseFolder.text = " ( æ‰“å¼€ç›®å½• )"
			)
		)
	)

	fn fnRefLikedFolderItems =
	(
		arrDirItems = #()
		if iniLikedFolder.count != 0 then
		(
			for i = iniLikedFolder.count to 1 by -1 do
			(
				if (doesDirectoryExist iniLikedFolder[i].dir) then 
				(
					append arrDirItems iniLikedFolder[i].name
				)
				else 
				(
					messagebox (iniLikedFolder[i].dir + "\r\næ–‡ä»¶å¤¹ä¸å­˜åœ¨\r\nå·²ä»å¸¸ç”¨è·¯å¾„ä¸­åˆ é™¤                                                                          \r\n")
					deleteitem iniLikedFolder i
				)
			)
		)
		else 
		(
			lblLikedFolder.text = "è¯·æ·»åŠ å¸¸ç”¨ç›®å½•"
		)
		rolBsOpenTools.ltbLikedFolder.items = arrDirItems
	)

	fn fnRefreshFilterItems =
	(
		local arrPreItems = #()
		for i = iniFilterPrefix.count to 1 by -1 do
		(
			append arrPreItems iniFilterPrefix[i]
		)
		rolBsOpenTools.ltbFilterPrefix.items = arrPreItems
	)

	fn fnLoadBip fBip =
	(
		-- biped.loadBipFile <biped_ctrl> <file_name> \
		-- [#matchFileStruct] [#zeroHgt] [#noRedraw] \
		-- [#loadMaxObjects][#promptForDuplicates] [#retargetHeight] \
		-- [#retargetLimbSizes] [#scaleIKObjectSize] [#loadSubAnimControllers] \
		-- [#loadSelectedMaxObjects nodename_array] \
		-- [#loadSelectedSubAnimControllers bipednodename_array int_array]

		local numSelbiped = #()   -------åˆ¤æ–­é€‰æ‹©äº†å‡ ä¸ªbipedéª¨æ¶
		if selection.count != 0 then
		(
			for i in (selection as array) where ((classof i == Biped_Object) and (i.ishidden == false)) do  
			(
				appendIfUnique numSelbiped i.controller.rootNode  --æ·»åŠ åˆ°éª¨æ¶æ•°ç»„
			)
			for b in numSelbiped do 
			(
				biped.loadBipFile b.controller fBip #noRedraw
			)
		)
	)

	fn fnRefreshDate =
	(
		local arrTime = (getLocalTime())
		local dayWeekID = (mod arrTime[3] 7) as integer
		lblDateTime.text = dateTime.Year as string + "/" + dateTime.Month as string + "/" \
		+ dateTime.Day as string + "  æ˜ŸæœŸ" + arrDayWeek[dayWeekID + 1] as string
	)

	-- é¢„è§ˆç›¸å…³å‡½æ•°
	fn fnTogglePreviewWindow state =
	(
		if state then
		(
			rolBsOpenTools.width = 950
			grpPreview.visible = true
			grpFileInfo.visible = true
			chePreview.text = "é¢„è§ˆ <<"
		)
		else
		(
			rolBsOpenTools.width = 580
			grpPreview.visible = false
			grpFileInfo.visible = false
			chePreview.text = "é¢„è§ˆ >>"
		)
	)

	-- è·å–Maxæ–‡ä»¶ç‰ˆæœ¬å·ï¼ˆè½¬æ¢ä¸º20xxæ ¼å¼ï¼‰
	fn fnGetMaxVersionString versionNumber =
	(
		-- ç›´æ¥ä½¿ç”¨ç‰ˆæœ¬å·ï¼Œä¸éœ€è¦è½¬æ¢
		return versionNumber as string
	)
	
	-- è·å–æ–‡ä»¶å±æ€§ä¿¡æ¯
	fn fnGetFileProperties filePath =
	(
		local fileExt = getFilenameType filePath
		local infoText = ""
		
		case fileExt of
		(
			".max":
			(
				-- Maxæ–‡ä»¶å±æ€§
				try
				(
					-- å°è¯•åŠ è½½MaxFileUtilitiesåº“
					local assemblyPath = (getDir #scripts) + "\\BulletScripts\\Res\\MaxFileUtilitiesDotNet.dll"
					if doesFileExist assemblyPath then
					(
						local assembly = dotnet.loadAssembly assemblyPath
						local mfuMFI = dotNetObject "MaxFileUtilities.MaxFileInterface"
						
						-- è·å–åŸºæœ¬ä¿¡æ¯
						local generalInfo = mfuMFI.GetGeneralInformation filePath
						local meshInfo = mfuMFI.GetMeshTotalsInformation filePath
						local sceneInfo = mfuMFI.GetSceneTotalsInformation filePath
						
						-- æ„å»ºä¿¡æ¯æ–‡æœ¬
						-- infoText = "Maxæ–‡ä»¶å±æ€§:\n"
						-- infoText += "Maxç‰ˆæœ¬å·: " + (generalInfo.MaxVersion as string) + "\n"
						if generalInfo.ReadableSavedAsVersion != undefined then
						(
							infoText += "ä¿å­˜ç‰ˆæœ¬: " + (generalInfo.ReadableSavedAsVersion as string) + "\n"
						)
						infoText += "\n"
						-- infoText += "åœºæ™¯ç»Ÿè®¡:\n"
						infoText += "ç‰©ä½“æ€»æ•°: " + (sceneInfo.Total as string) + "\n"
						infoText += "å‡ ä½•ä½“: " + (sceneInfo.NbOfObjects as string) + "  ||  "
						infoText += "å½¢çŠ¶: " + (sceneInfo.NbOfShapes as string) + "  ||  "
						infoText += "ç¯å…‰: " + (sceneInfo.NbOfLights as string) + "  ||  "
						infoText += "ç›¸æœº: " + (sceneInfo.NbOfCameras as string) + "  ||  "
						infoText += "è¾…åŠ©å¯¹è±¡: " + (sceneInfo.NbOfHelpers as string) + "  ||  "
						infoText += "ç©ºé—´æ‰­æ›²: " + (sceneInfo.NbOfSpaceWarps as string) + "  ||  "
												
						-- å°è¯•è·å–éª¨éª¼ä¿¡æ¯
						try
						(
							-- è·å–æ‰€æœ‰å¯¹è±¡åç§°ï¼ŒæŸ¥æ‰¾éª¨éª¼
							local allObjects = mfuMFI.GetObjectsNames filePath
							local boneCount = 0
							for objName in allObjects do
							(
								-- æ£€æŸ¥æ˜¯å¦åŒ…å«éª¨éª¼ç›¸å…³å…³é”®è¯
								if matchPattern objName pattern:"*Bone*" or matchPattern objName pattern:"*Bip*" or matchPattern objName pattern:"*Armature*" then
								(
									boneCount += 1
								)
							)
							-- infoText += "éª¨éª¼ç»Ÿè®¡:\n"
							infoText += "éª¨éª¼æ•°é‡: " + (boneCount as string) + "\n\n"
						)
						catch
						(
							-- infoText += "éª¨éª¼ç»Ÿè®¡:\n"
							infoText += "éª¨éª¼æ•°é‡: æ— æ³•è·å–\n\n"
						)
						
						-- infoText += "ç½‘æ ¼ç»Ÿè®¡:\n"
						infoText += "é¡¶ç‚¹æ•°: " + (meshInfo.NbOfVertices as string) + "  ||  "
						infoText += "é¢æ•°: " + (meshInfo.NbOfFaces as string) + "\n\n"
					)
					else
					(
						infoText = "æœªæ‰¾åˆ°MaxFileUtilitiesåº“\næ— æ³•è·å–è¯¦ç»†å±æ€§\n\n"
					)
				)
				catch
				(
					infoText = "è·å–Maxæ–‡ä»¶å±æ€§å¤±è´¥\n" + (getCurrentException()) + "\n\n"
				)
			)
			".fbx":
			(
				-- FBXæ–‡ä»¶å±æ€§
				infoText = "FBXæ–‡ä»¶å±æ€§:\n"
				infoText += "æ–‡ä»¶ç±»å‹: FBXæ ¼å¼\n\n"
			)
			".bip":
			(
				-- BIPæ–‡ä»¶å±æ€§
				infoText = "BIPæ–‡ä»¶å±æ€§:\n"
				infoText += "æ–‡ä»¶ç±»å‹: BipedåŠ¨ç”»\n\n"
			)
			".ms":
			(
				-- MSæ–‡ä»¶å±æ€§
				infoText = "MSæ–‡ä»¶å±æ€§:\n"
				infoText += "æ–‡ä»¶ç±»å‹: MAXScriptè„šæœ¬\n\n"
			)
			default:
			(
				infoText = "æœªçŸ¥æ–‡ä»¶ç±»å‹\n\n"
			)
		)
		
		-- æ·»åŠ é€šç”¨æ–‡ä»¶ä¿¡æ¯
		try
		(
			-- ä½¿ç”¨dotNet FileInfoè·å–æ–‡ä»¶ä¿¡æ¯
			local fileInfo = dotNetObject "System.IO.FileInfo" filePath
			if fileInfo != undefined and fileInfo.Exists then
			(
				-- è·å–æ–‡ä»¶å¤§å°
				local fileSize = fileInfo.Length
				local fileSizeKB = fileSize / 1024.0
				local fileSizeMB = fileSize / (1024.0 * 1024.0)
				local sizeText = if fileSizeMB >= 1.0 then 
				(
					-- ä¿ç•™ä¸€ä½å°æ•°æ˜¾ç¤ºMB
					((fileSizeMB * 10) as integer / 10.0) as string + " MB"
				) else 
				(
					-- ä¿ç•™ä¸€ä½å°æ•°æ˜¾ç¤ºKB
					((fileSizeKB * 10) as integer / 10.0) as string + " KB"
				)
				infoText += "æ–‡ä»¶å¤§å°: " + sizeText + "  ||  "
				
				-- è·å–ä¿®æ”¹æ—¶é—´
				local lastWriteTime = fileInfo.LastWriteTime
				local timeString = lastWriteTime.ToString "yyyy/MM/dd HH:mm:ss"
				infoText += "ä¿®æ”¹æ—¶é—´: " + timeString
			)
			else
			(
				infoText += "æ–‡ä»¶å¤§å°: æ— æ³•è·å–\n"
				infoText += "ä¿®æ”¹æ—¶é—´: æ— æ³•è·å–"
			)
		)
		catch
		(
			infoText += "æ–‡ä»¶å¤§å°: è·å–å¤±è´¥\n"
			infoText += "ä¿®æ”¹æ—¶é—´: è·å–å¤±è´¥"
		)
		
		return infoText
	)

	fn fnPreviewFile filePath =
	(
		if filePath == undefined or filePath == "" then return false
		
		-- æ˜¾ç¤ºæ–‡ä»¶å±æ€§
		local propertiesText = fnGetFileProperties filePath
		lblFileInfo.text = propertiesText
		grpFileInfo.visible = true
		
		local fileExt = getFilenameType filePath
		case fileExt of
		(
			".max":
			(
				-- Maxæ–‡ä»¶é¢„è§ˆç¼©ç•¥å›¾
				try
				(
					-- å°è¯•è·å–Maxæ–‡ä»¶çš„ç¼©ç•¥å›¾
					local thumbnailFound = false
					
					-- ä½¿ç”¨ä¸“é—¨çš„dotNetåº“è·å–Maxæ–‡ä»¶ç¼©ç•¥å›¾
					try
					(
						-- å°è¯•åŠ è½½MaxFileUtilitiesåº“
						local assemblyPath = (getDir #scripts) + "\\BulletScripts\\Res\\MaxFileUtilitiesDotNet.dll"
						if doesFileExist assemblyPath then
						(
							local assembly = dotnet.loadAssembly assemblyPath
							local mfuMFI = dotNetObject "MaxFileUtilities.MaxFileInterface"
							
							-- è·å–ç¼©ç•¥å›¾
							local thumb = mfuMFI.GetThumbnail filePath
							if thumb != undefined then
							(
								-- ç›´æ¥ä½¿ç”¨dotNet bitmapï¼Œè®¾ç½®SizeModeç¡®ä¿å›¾ç‰‡å®Œå…¨æ˜¾ç¤º
								dncMaxPreview.SizeMode = (dotNetClass "System.Windows.Forms.PictureBoxSizeMode").Zoom
								dncMaxPreview.Image = thumb
								thumbnailFound = true
							)
						)
					)
					catch
					(
						-- å¦‚æœè·å–ç¼©ç•¥å›¾å¤±è´¥ï¼Œè®°å½•é”™è¯¯ä¿¡æ¯
						print "è·å–Maxæ–‡ä»¶ç¼©ç•¥å›¾å¤±è´¥: " + (getCurrentException())
					)
					
					-- å¦‚æœæ²¡æœ‰æ‰¾åˆ°ç¼©ç•¥å›¾ï¼Œæ˜¾ç¤ºæ–‡ä»¶ä¿¡æ¯
					if not thumbnailFound then
					(
						-- æ¸…é™¤å›¾ç‰‡
						dncMaxPreview.Image = undefined
					)
				)
				catch
				(
					-- æ¸…é™¤å›¾ç‰‡
					dncMaxPreview.Image = undefined
				)
			)
			".fbx":
			(
				-- FBXæ–‡ä»¶é¢„è§ˆ - ç›´æ¥filein Pythonè„šæœ¬
				dncMaxPreview.visible = true
				
				local pythonScriptPath = (getDir #scripts) + "\\BulletScripts\\BsAnimLib.py"
				
				if doesFileExist pythonScriptPath then
				(
					-- å…ˆå°è¯•é”€æ¯ä¹‹å‰çš„FBXæŸ¥çœ‹å™¨çª—å£
					try
					(
						python.execute ("if 'destroy_fbx_viewer' in globals(): destroy_fbx_viewer()")
					)
					catch
					(
						print "é”€æ¯ä¹‹å‰çš„FBXæŸ¥çœ‹å™¨å¤±è´¥ï¼Œç»§ç»­åˆ›å»ºæ–°çª—å£"
					)
					
					-- è®¾ç½®å…¨å±€å˜é‡å­˜å‚¨æ–‡ä»¶è·¯å¾„
					python.execute ("FBX_FILE_PATH = r'" + filePath + "'")
					
					-- åŠ è½½å¹¶æ‰§è¡ŒPythonè„šæœ¬
					python.ExecuteFile pythonScriptPath
				)
				else
				(
					-- å›é€€åˆ°å¤–éƒ¨FBX Review
					local fbxReviewPath = (getDir #scripts) + "\\BulletScripts\\Res\\fbxreview.exe"
					if doesFileExist fbxReviewPath then
					(
						shellLaunch fbxReviewPath filePath
					)
				)
			)
			".bip":
			(
				-- æ¸…é™¤å›¾ç‰‡
				dncMaxPreview.Image = undefined
			)
			".ms":
			(
				-- æ¸…é™¤å›¾ç‰‡
				dncMaxPreview.Image = undefined
			)
			default:
			(
				-- æ¸…é™¤å›¾ç‰‡
				dncMaxPreview.Image = undefined
			)
		)
	)

	fn fnGetSelectedFilePath =
	(
		local selectedID = ltbFilesList.selection
		if selectedID == 0 then return undefined
		
		local idNew = selectedID
		if ckbReverse.state == true then 
		(
			idNew = (arrFiles.count + arrFolders.count) + 1 - selectedID
		)
		
		if idNew > arrFolders.count then
		(
			local idFile = idNew - arrFolders.count
			return arrFiles[idFile]
		)
		return undefined
	)
	
	----------------------------------------------------------------------------------------------------
	
	on rolBsOpenTools open do 
	(
		-- sldSetWidth.value        = iniWidthOpenTools
		-- rolBsOpenTools.width     = iniWidthOpenTools
		btnRefreshFolder.images  = #("UVWUnwrapModes_16i.bmp","UVWUnwrapModes_16i.bmp",28,3,3,3,3,true,false)
		-- ckbLockWidth.images      = #("LockButtonExt_i.bmp","LockButtonExt_i.bmp",2,1,1,2,2,true,false)
		btnAddPathToVavorite.images = #("enss_tools_16i.bmp","enss_tools_16a.bmp",13,5,5,6,6,false,true)
		btnOpenCurrentDir.images = #("bip_general_i.bmp","bip_general_i.bmp",30,5,5,6,6,false,true)
		-- btnPrevFolder.images     = #("MergeAnim_24i.bmp","MergeAnim_24i.bmp",4,1,1,1,1,false,true)
		
		-- åˆå§‹åŒ–é¢„è§ˆçª—å£
		grpPreview.visible = false
		grpFileInfo.visible = false
		chePreview.state = false
		
		-- è®¾ç½®PictureBoxçš„SizeModeï¼Œç¡®ä¿å›¾ç‰‡èƒ½å¤Ÿå®Œæ•´æ˜¾ç¤º
		dncMaxPreview.SizeMode = (dotNetClass "System.Windows.Forms.PictureBoxSizeMode").Zoom
		
		stLoadConfigAll.fnLoadConfigBsOpenTools ()  ---------------è„šæœ¬ä½ç½®ç­‰èµ‹å€¼
		stSetConfigAll.fnSetConfigBsOpenTools ()  ----------------ä¿å­˜ä½ç½®ä¿¡æ¯åˆ°iniæ–‡ä»¶	
		fnRefLikedFolderItems ()
		fnRefreshAddress ()
		fnRefreshList maxFilePath type: arrFileType[rdoFileType.state]
		lblCountTips.text = "æ–‡ä»¶ï¼š" + arrFiles.count as string
		lblLikedFolder.text = if ltbLikedFolder.selected == undefined then "è¯·æ·»åŠ å¸¸ç”¨ç›®å½•" else ltbLikedFolder.selected
		ltbFilterPrefix.selection = 0
		fnRefreshFilterItems ()
		ltbLikedFolder.selection = iniSelectedID
		fnRefreshDate ()
	)

	on rolBsOpenTools close do -- å…³é—­è®°å¿†æµ®åŠ¨çª—å£ä½ç½®
	(
		iniPosOpenTools   = (GetDialogPos rolBsOpenTools)
		iniSelectedID     = rolBsOpenTools.ltbLikedFolder.selection
		iniWidthOpenTools = rolBsOpenTools.width
		stSetConfigAll.fnSetConfigBsOpenTools ()
	)

	on rolBsOpenTools mbuttondown pos do 
	(
		try (destroydialog rolBsOpenTools) catch ()
		try(destroydialog rolAddItem)catch()
	)

	on rolBsOpenTools lbuttondown posMou do
	(
		posMouMove = posMou
		switchMouState = on
	)

	on rolBsOpenTools lbuttonup posMou do
	(
		switchMouState = off
	)

	on rolBsOpenTools rbuttondown pos do 
	(
		tempLastFolder = fnGetLastFolder edtBrowseFolder.text
		fnRefreshList tempLastFolder type:arrFileType[rdoFileType.state]
		rolBsOpenTools.ltbFilesList.selection = 0
	)

	on rolBsOpenTools mouseMove pos do
	(
		if switchMouState == on then
		(
			SetDialogPos rolBsOpenTools (mouse.screenpos - posMouMove)			
		)
	)

	-- on rolBsOpenTools resized pos do
	-- (
	-- 	grpMain.width          = rolBsOpenTools.width - 10
	-- 	grpDate.width          = rolBsOpenTools.width - 10
	-- 	edtBrowseFolder.width  = rolBsOpenTools.width - 130
	-- 	ltbFilesList.width     = rolBsOpenTools.width - 95
	-- 	btnOpenCurrentDir.pos  = [rolBsOpenTools.width - 35,20]
	-- 	btnPrevFolder.pos      = [rolBsOpenTools.width - 60,20]

	-- )
	------------------------------------------------------------------------------

	-- on ckbLockWidth changed state do 
	-- (
	-- 	if state == on then sldSetWidth.enabled = true 
	-- 	else sldSetWidth.enabled = false
	-- )

	-- on ckbLockWidth rightclick do
	-- (
	-- 	rolBsOpenTools.width = 340
	-- 	sldSetWidth.value    = 340
	-- )

	-- on sldSetWidth changed ticks do
	-- (
	-- 	rolBsOpenTools.width = ticks
	-- )

	on btnRefreshFolder pressed do 
	(
		fnRefreshAddress ()
		fnRefreshList edtBrowseFolder.text type:arrFileType[rdoFileType.state]
	)

	on btnPrevFolder pressed do 
	(
		tempLastFolder = fnGetLastFolder edtBrowseFolder.text
		fnRefreshList tempLastFolder type:arrFileType[rdoFileType.state]
		rolBsOpenTools.ltbFilesList.selection = 0
	)

	on btnRefreshFolder rightclick do 
	(
		if CheckForSave() then 
		(
			resetMaxFile #noPrompt
			max tool maximize
		)
	)

	on btnOpenCurrentDir pressed do 
	(
		if (doesDirectoryExist edtBrowseFolder.text) then
		(
			shellLaunch edtBrowseFolder.text ""
		)
	)

	on edtBrowseFolder changed txt do
	(
		fnRefreshList txt type:arrFileType[rdoFileType.state]
	)

	on rdoFileType changed state do
	(
		fnRefreshList rolBsOpenTools.edtBrowseFolder.text type:arrFileType[state]
	)

	on btnOpenAddress pressed do 
	(
		dirOpened = getSavePath caption:"è¯·é€‰æ‹©Maxæ–‡ä»¶è·¯å¾„:" initialDir:(maxFilePath)
		if (dirOpened != undefined) then
		(
			edtBrowseFolder.text = dirOpened
			fnRefreshList dirOpened type:arrFileType[rdoFileType.state]
		)
	)
	
	on btnAddLikedFolder pressed do 
	(
		try(destroydialog rolAddItem)catch()
		createdialog rolAddItem fgcolor:myfgColor pos:mouse.screenpos
	)

	on btnDelLikedFolder pressed do
	(
		if iniLikedFolder.count > 0 and ltbLikedFolder.selection != 0 then
		(
			idDel = iniLikedFolder.count + 1 - ltbLikedFolder.selection
			local delName = iniLikedFolder[idDel].name
			local confirmResult = queryBox ("ç¡®å®šè¦åˆ é™¤å¸¸ç”¨ç›®å½• \"" + delName + "\" å—?") title:"åˆ é™¤ç¡®è®¤" beep:false
			if confirmResult then
			(
				deleteItem iniLikedFolder idDel
				print idDel
				fnRefLikedFolderItems ()
			)
		)
	)

	on btnAddPathToVavorite pressed do 
	(
		try(destroydialog rolAddItem)catch()
		createdialog rolAddItem fgcolor:myfgColor pos:mouse.screenpos
		
		if (edtBrowseFolder.text != "") then
		(
			rolAddItem.edtDirStr.text = edtBrowseFolder.text
			rolAddItem.fnAddToFavorite()
		)
	)

	on btnAddFilterPrefix pressed do 
	(
		if (edtInputFilter.text != "") then
		(
			if (findItem iniFilterPrefix edtInputFilter.text) != 0 then 
			(
				messagebox "-------------------------------------\r\nå¯èƒ½è¿‡æ»¤è¯ç¼€ä¸­å·²å­˜åœ¨~"
			)
			else
			(
				append iniFilterPrefix edtInputFilter.text
				arrPreItems = #()
				for i = iniFilterPrefix.count to 1 by -1 do
				(
					append arrPreItems iniFilterPrefix[i]
				)
				rolBsOpenTools.ltbFilterPrefix.items = arrPreItems
				rolBsOpenTools.ltbFilterPrefix.selection = 1
			)
			rolBsOpenTools.fnRefreshList rolBsOpenTools.edtBrowseFolder.text type:arrFileType[rolBsOpenTools.rdoFileType.state]
		)
		else 
		(
			messagebox "--------------------------------------\r\nè¯·åœ¨æŒ‰é’®ä¸‹æ–¹è¾“å…¥è¿‡æ»¤è¯ç¼€~\r\n--------------------------------------"
		)
	)

	on btnDelFilterPrefix pressed do 
	(
		if iniFilterPrefix.count > 0 and ltbFilterPrefix.selection != 0 then
		(
			idDel = iniFilterPrefix.count + 1 - ltbFilterPrefix.selection
			local delPrefix = iniFilterPrefix[idDel]
			local confirmResult = queryBox ("ç¡®å®šè¦åˆ é™¤è¿‡æ»¤è¯ç¼€ \"" + delPrefix + "\" å—?") title:"åˆ é™¤ç¡®è®¤" beep:false
			if confirmResult then
			(
				deleteItem iniFilterPrefix idDel
				fnRefreshFilterItems ()
				fnRefreshList edtBrowseFolder.text type:arrFileType[rdoFileType.state]
			)
		)
	)

	on ltbLikedFolder selected id do
	(
		if iniLikedFolder[id] != undefined then
		(
			strDir = iniLikedFolder[iniLikedFolder.count + 1 - id].dir
			fnRefreshList strDir type:arrFileType[rdoFileType.state]
		)
		iniSelectedID = id
	)
	
	on ltbLikedFolder rightclick id do
	(
		btnDelLikedFolder.pressed()
	)

	on ltbFilterPrefix selected id do
	(
		fnRefreshList edtBrowseFolder.text type:arrFileType[rdoFileType.state]
	)
	
	on ltbFilterPrefix rightclick id do
	(
		btnDelFilterPrefix.pressed()
	)

	on ltbFilesList rightclick id do
	(
		tempLastFolder = fnGetLastFolder edtBrowseFolder.text
		fnRefreshList tempLastFolder type:arrFileType[rdoFileType.state]
		rolBsOpenTools.ltbFilesList.selection = 0
	)

	on ltbFilesList doubleClicked id do
	(
		if (doesDirectoryExist edtBrowseFolder.text) then
		(
			idNew = id
			if rolBsOpenTools.ckbReverse.state == true then 
			(
				idNew = (arrFiles.count + arrFolders.count) + 1 - id
			)
			-- if edtBrowseFolder.text == (getdir #preview) then ramplayer arrFiles[idNew] ""
			-- else 
			-- (
				case of
				(
					((idNew <= arrFolders.count) and (idNew > 0)):
					(
						edtBrowseFolder.text = arrFolders[idNew]
						fnRefreshList edtBrowseFolder.text type:arrFileType[rdoFileType.state]
						-- fnRefreshList edtBrowseFolder.text type:arrFileType[rdoFileType.state]
					)
					(idNew > arrFolders.count):
					(
						local idFile = idNew - arrFolders.count
						case of 
						(
							(rdoFileType.state == 1):
							(
								if CheckForSave() then 
								(
									loadMaxFile arrFiles[idFile] useFileUnits:true quiet:ckbSilentOpen.state
								)
							)
							(rdoFileType.state == 2):(importFile arrFiles[idFile] #noPrompt using:FBXIMP)
							(rdoFileType.state == 3):(fnLoadBip arrFiles[idFile])
							(rdoFileType.state == 4):(fileIn arrFiles[idFile])
						)
					)
				)
			-- )
		)
		else 
		(
			if edtBrowseFolder.text == "( æœ€è¿‘æ‰“å¼€ Max æ–‡ä»¶åˆ—è¡¨ )" then
			(
				if doesFileExist arrRecentFiles[id] then 
				(
					loadMaxFile (arrRecentFiles[id]) useFileUnits:true quiet:ckbSilentOpen.state
					local tempDir = getfilenamepath arrRecentFiles[id]
					fnRefreshList tempDir type:arrFileType[rdoFileType.state]
				)
			)
		)
	)

	on btnDesktopDir pressed do 
	(
		if iniDesktop.dir == "" then 
		(
			iniDesktop.dir = (@"C:\Users\" + (filterString  (getdir #userscripts) @"\")[3] + @"\Desktop\")
		)
		fnRefreshList iniDesktop.dir type:arrFileType[rdoFileType.state]
	)

	on btnDesktopDir rightclick do 
	(
		dirOpened = getSavePath caption:"è¯·è®¾ç½®æ¡Œé¢è·¯å¾„:" initialDir:(maxFilePath)
		if (dirOpened != undefined) then
		(
			iniDesktop.dir = dirOpened
			fnRefreshList iniDesktop.dir type:arrFileType[rdoFileType.state]
		)
	)

	on btnAutobackDir pressed do 
	(
		rdoFileType.state  = 1
		fnRefreshList (getdir #autoback) type:".max"
	)

	on btnScriptsDir pressed do
	(
		rdoFileType.state  = 4
		fnRefreshList (getdir #scripts) type:".ms*"
	)

	on btnScriptsDir rightclick do
	(
		rdoFileType.state  = 4
		fnRefreshList (getdir #startupScripts) type:".ms*"
	)

	on btnRecentFileDir pressed do 
	(
		fnLoadRecentFileList ()
		for i in arrRecentFiles do 
		(
			if doesFileExist i then
			(
				append arrRecentFilesName ("ğŸ“„ " + (getFilenameFile i) + ".max")
			)
			else append arrRecentFilesName ("ğŸ“„ ( X )  " + (getFilenameFile i) + ".max")
		)
		rdoFileType.state  = 1
		ltbFilesList.items = arrRecentFilesName
		edtBrowseFolder.text = "( æœ€è¿‘æ‰“å¼€ Max æ–‡ä»¶åˆ—è¡¨ )"
		lblCountTips.text = "æ–‡ä»¶ï¼š" + arrRecentFiles.count as string
		lblLikedFolder.text = if ltbLikedFolder.selected == undefined then "è¯·æ·»åŠ å¸¸ç”¨ç›®å½•" else ltbLikedFolder.selected
	)

	on btnClose pressed do 
	(
		try (destroydialog rolBsOpenTools) catch()
	)
	
	-- æ–‡ä»¶æ“ä½œæŒ‰é’®äº‹ä»¶
	on btnCopyPath pressed do
	(
		local selectedFile = fnGetSelectedFilePath()
		if selectedFile != undefined and doesFileExist selectedFile then
		(
			try
			(
				-- ä½¿ç”¨dotNetå‰ªè´´æ¿åŠŸèƒ½
				local clipboard = dotNetClass "System.Windows.Forms.Clipboard"
				clipboard.SetText selectedFile
				messageBox "æ–‡ä»¶è·¯å¾„å·²å¤åˆ¶åˆ°å‰ªè´´æ¿ï¼" title:"å¤åˆ¶æˆåŠŸ"
			)
			catch
			(
				messageBox "å¤åˆ¶å¤±è´¥ï¼š" + (getCurrentException()) title:"å¤åˆ¶å¤±è´¥"
			)
		)
		else
		(
			messageBox "è¯·å…ˆé€‰æ‹©ä¸€ä¸ªæ–‡ä»¶ï¼" title:"æç¤º"
		)
	)
	
	on btnCopyFileName pressed do
	(
		local selectedFile = fnGetSelectedFilePath()
		if selectedFile != undefined and doesFileExist selectedFile then
		(
			try
			(
				local fileName = getFilenameFile selectedFile + getFilenameType selectedFile
				-- ä½¿ç”¨dotNetå‰ªè´´æ¿åŠŸèƒ½
				local clipboard = dotNetClass "System.Windows.Forms.Clipboard"
				clipboard.SetText fileName
				messageBox "æ–‡ä»¶åå·²å¤åˆ¶åˆ°å‰ªè´´æ¿ï¼" title:"å¤åˆ¶æˆåŠŸ"
			)
			catch
			(
				messageBox "å¤åˆ¶å¤±è´¥ï¼š" + (getCurrentException()) title:"å¤åˆ¶å¤±è´¥"
			)
		)
		else
		(
			messageBox "è¯·å…ˆé€‰æ‹©ä¸€ä¸ªæ–‡ä»¶ï¼" title:"æç¤º"
		)
	)

	on edtInputFilter entered textFolder do
	(
		fnRefreshList edtBrowseFolder.text filename:textFolder type:arrFileType[rdoFileType.state]
	)

	on ckbReverse changed state do 
	(
		rolBsOpenTools.fnRefreshList edtBrowseFolder.text type:arrFileType[rdoFileType.state]
		arrAll = (arrFoldersName + arrFilesName)
		id = rolBsOpenTools.ltbFilesList.selection
		rolBsOpenTools.ltbFilesList.selection = arrAll.count + 1 - id
	)

	-- é¢„è§ˆç›¸å…³äº‹ä»¶
	on chePreview changed state do
	(
		fnTogglePreviewWindow state
		if state and ltbFilesList.selection != 0 then
		(
			local selectedFile = fnGetSelectedFilePath()
			if selectedFile != undefined then
			(
				fnPreviewFile selectedFile
			)
		)
	)

	on ltbFilesList selected id do
	(
		if chePreview.state and id != 0 then
		(
			local selectedFile = fnGetSelectedFilePath()
			if selectedFile != undefined then
			(
				fnPreviewFile selectedFile
			)
		)
	)
)
if (iniPosOpenTools != 0) then (Createdialog rolBsOpenTools fgcolor:myFgColor pos:iniPosOpenTools style:#() lockHeight:true)
else (Createdialog rolBsOpenTools fgcolor:myFgColor style:#() lockHeight:true)
------------------------toolbar----------------------------------------------------
macroScript BsOpenTools
category:"_[BulletTools]"
buttonText:"æ—¶å…‰æœº"
toolTip:"æ—¶å…‰æœº"
-- Icon:#("Systems",2)
(
	on execute do
	(
		fileIn ((getDir #Scripts)+ @"\\BulletScripts\\BsOpenTools.ms")
	)
)
-------------------------------------------------------------------------------------